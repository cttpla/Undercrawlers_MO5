10 ' ****************************
20 '
30 ' UNDERCRAWLERS v1.0
40 ' Paolo Cattaneo 2K19
50 ' 
60 ' Shared demo game for  
70 ' 8-Bit Retroprogramming ITA
80 ' Facebook Group
90 '
95 ' ****************************
97 '
99  CLEAR 5000,,128  
100 CONSOLE 0,24,0:CLS:SCREEN 7,0,0
101 GOSUB 30000:GOSUB 50000 'LOAD CHARDATA, 'LOAD LAB
105 POKE &H2076,255               'KEYBOARD DELAY
110 POKE &H2019,PEEK(&H2019) OR 8 'NO BUZZER
330 LOCATE 0,24
335 CONSOLE 0,24,0
345 LOCATE 0,0:COLOR 3:PRINT ANK$+" Undercrawlers v1.0 "+ANK$:PRINT CHR$(20):CONSOLE 16,24,0
340 COLR=2:COLR2=3:COLR3=2
350 'BOX(0,10)-(180,120),2
360 '
370 GOSUB 17000 'PARTY
400 GOTO 25000
500 'DUNGEON: Player Setup 
510 '1=far,2=mid,3=close
520 FOR I=1 TO 3:BETM(I)=0:NEXT
530 BVAL1=0:PLRDIR=1:PLRPSX=2:PLRPSY=8 'INITIAL POS
540 GOTO 1000
550 ' 
560 '*********** Select labyrinth view
570 FOR Y=1 TO 3:FOR X=1 TO 3
580 TILX=PLRPSX+COORDX(X,Y,PLRDIR):IF TILX<1 OR TILX>MAPW THEN TILX=0
590 TILY=PLRPSY+COORDY(X,Y,PLRDIR):IF TILY<1 OR TILY>MAPH THEN TILY=0  
600 TILEVIEW(X,Y)=MAP(TILX,TILY,1)
610 NEXT X:NEXT Y
620 RETURN 
1000 'DRAW CUR.POS 
1005 GOSUB 560
1010 PLRLOC=1:BVAL1=ETMVL1(ETMS(PLRPSX,PLRPSY,PLRDIR,1)):BETM(3)=ETMID(ETMS(PLRPSX,PLRPSY,PLRDIR,1))
1011 TILX=PLRPSX+MOVF(1,PLRDIR):IF TILX<1 OR TILX>MAPW THEN TILX=0
1012 TILY=PLRPSY+MOVF(2,PLRDIR):IF TILY<1 OR TILY>MAPH THEN TILY=0  
1013 BETM(2)=ETMID(ETMS(TILX,TILY,PLRDIR,1)):TILX=TILX*2:TILY=TILY*2:IF TILX<1 OR TILX>MAPW THEN TILX=0  
1015 IF TILY<1 OR TILY>MAPH THEN TILY=0
1017 BETM(1)=ETMID(ETMS(TILX,TILY,PLRDIR,1))
1020 COLR=CWALL:CCHEST=15:GOSUB 11200 'DRAW LAB 
1050 'PRINT PLRPSX;PLRPSY
1060 COLOR CWHITE:LOCATE 26,0,0:PRINT "Dir. "+D$(PLRDIR):COLOR CNORM:LOCATE 0,24,1 'GOSUB 17000 'PARTY
1170 'IF BOSS OR OTHER EVENT=NO MONSTER SPAWN ROLL; ELSE=ROLL FOR SPAWN MONSTERS
1175 IF BETM(3)=3 THEN GOSUB 40500 ELSE IF ROOM1=1 AND RSEED+RND*(10)>=FCHN+BFCHN THEN GOSUB 40500 'COMBAT! 'BFCHN=bonus fight chance
1180 ISBTL=0:IF FCHN>0 THEN FCHN=FCHN-1: IF ROOM1=1 GOTO 1200 'FCHN = Fight CHance
1190 LOCATE 0,24,1:PRINT "Entering the dungeon you feel"+CHR$(13)+CHR$(10)+"a sense of danger.":ROOM1=1:PRINT
'1200 A$="":K$=INKEY$:RSEED=RND:PLAY "A05L016O2"+MUSIC$(CBGMN):CBGMN=CBGMN+1:IF CBGMN>16 THEN CBGMN=1
1200 COLOR CNORM:LOCATE 0,24,1:PRINT:PRINT ANK$;
1205 A$="":K$=INKEY$:RSEED=RND
1230 IF K$=CHR$(11) THEN GOTO 1500   'UP
1250 IF K$=CHR$(10) THEN GOTO 1530   'DOWN
1240 IF K$=CHR$(9) THEN GOTO 1560    'LEFT
1245 IF K$=CHR$(8) THEN GOTO 1700    'RIGHT
1255 IF K$="Q" THEN GOTO 60000 ELSE IF K$="I" THEN PLAY OK$:GOSUB 57000:GOTO 1200 ELSE IF K$="S" THEN PLAY OK$:GOSUB 55300:GOTO 1200
1260 IF K$="" THEN GOTO 1205
1265 ISSPMN=0: IF K$="C" THEN PLAY OK$:GOSUB 55200:GOTO 1200 'SPELL
'1270 'IF K$="M" THEN GOTO 55000
1276 GOTO 1205 'non ha premuto niente
1280 COLR=-1:CCHEST=-1 'ERASE
'1290 CONSOLE 1,16,0:CLS:CONSOLE 16,24,0:LOCATE 0,24 
1290 GOSUB 11200
1300 IF A$<>"" THEN LOCATE 0,24,0:PRINT:PRINT:PRINT "PARTY "+A$
1400 GOTO 1000 'GET VIEW AND DRAW
1500 'Move forward
1505 IF BETM(3)=1 AND ETMVL1(ETMS(PLRPSX,PLRPSY,PLRDIR,1))=1 THEN CLS:RETURN 'EXIT DUNGEON
1510 IF MAP(PLRPSX+MOVF(1,PLRDIR),PLRPSY+MOVF(2,PLRDIR),1)=1 THEN GOTO 1750  
1520 PLRPSX=PLRPSX+MOVF(1,PLRDIR):PLRPSY=PLRPSY+MOVF(2,PLRDIR):A$="moves FORWARD":GOTO 1280
1530 'Move backward
1540 IF MAP(PLRPSX+MOVB(1,PLRDIR),PLRPSY+MOVB(2,PLRDIR),1)=1 THEN GOTO 1750 
1550 PLRPSX=PLRPSX+MOVB(1,PLRDIR):PLRPSY=PLRPSY+MOVB(2,PLRDIR):A$="moves BACKWARD":GOTO 1280
1560 'Turn east 
1570 PLRDIR=PLRDIR+1:IF PLRDIR>4 THEN PLRDIR=1
1590 A$="turns EAST":GOTO 1280
1700 'Turn west 
1720 PLRDIR=PLRDIR-1:IF PLRDIR<=0 THEN PLRDIR=4
1740 A$="turns WEST":GOTO 1280
1750 COLOR 1:LOCATE 0,24:SCREEN,,1:PLAY"A0L3O2DO":PRINT"> OUCH!<":SCREEN,,0:COLOR 3
1760 GOTO 1200
4900'
4910 '+++++++++++++++++++
4920 '+ LABIRINTH TILES +
4930 '+++++++++++++++++++
4940
5000 ' *** WALL : " * "
5010 ' *** no CLOSE VIEW
5020 RETURN
5030 '******* MEDIUM VIEW
5050 BOX(20,20)-(160,100),COLR
5070 RETURN
5080 '******* FAR VIEW
5100 IF TILEVIEW(2,2)<> 1 THEN BOX(60,50)-(120,70),COLR
5105 IF BETM(2)=1 THEN BOX(84,58)-(96,70),COLR ELSE IF BETM(2)=2 THEN BOXF(85,65)-(95,73),CCHEST  ELSE IF BETM(2)=4 THEN BOXF(88,60)-(93,73),COLR
5110 'IF TILEVIEW(1,2)=0 THEN LINE(0,70)-(60,70),COLR
5120 'IF TILEVIEW(3,2)=0 THEN LINE(120,70)-(180,70),COLR
5300 RETURN
6000 ' *** SIDE WALLS : "* *"
6010 ' *** CLOSE VIEW
6020 'PARETESX "*  ": PARETEDX "  *"
6040 LINE(0,10)-(20,20),COLR    'Obliqua su
6050 LINE(0,120)-(20,100),COLR  'Obliqua giu
6060 LINE(20,20)-(20,100),COLR  'Verticale
6070 RETURN
6080 LINE(180,10)-(160,20),COLR
6090 LINE(180,120)-(160,100),COLR
6100 LINE(160,20)-(160,100),COLR
6110 RETURN
6120 ' ******* MEDIUM VIEW
6125 'pareteSX
6130 IF TILEVIEW(2,2)=1 THEN GOTO 6161
6142 LINE(20,20)-(60,50),COLR
6150 LINE(20,100)-(60,70),COLR
6160 LINE(60,50)-(60,70),COLR
6161 IF TILEVIEW(1,3)=1 THEN GOTO 6170
6162 LINE(20,20)-(0,20),COLR
6165 LINE(20,100)-(0,100),COLR
6167 LINE(20,20)-(20,100),COLR
6170 RETURN
6180 'pareteDX
6182 IF TILEVIEW(2,2)=1 THEN GOTO 6201
6185 LINE(160,20)-(120,50),COLR
6190 LINE(160,100)-(120,70),COLR
6200 LINE(120,50)-(120,70),COLR
6201 IF TILEVIEW(3,3)=1 THEN GOTO 6210
6203 LINE(160,20)-(160,100),COLR
6205 LINE(160,20)-(180,20),COLR
6207 LINE(160,100)-(180,100),COLR
6210 RETURN
6220 ' ******* FAR VIEW
6230 'pareteSX
6260 IF TILEVIEW(2,2)=1 OR TILEVIEW(2,1)=1 THEN GOTO 6272
6262 LINE(60,50)-(85,57),COLR 
6265 LINE(60,70)-(85,63),COLR
6270 LINE(85,57)-(85,63),COLR
6272 'IF TILEVIEW(1,2)=0 THEN BOX(60,50)-(20,70),COLR 
6273 IF TILEVIEW(1,3)=1 AND TILEVIEW(1,2)=0 AND TILEVIEW(2,2)=0 THEN BOX(20,50)-(60,70),COLR ELSE IF TILEVIEW(2,2)=1 AND TILEVIEW(1,2)=0 AND TILEVIEW(1,3)=0 THEN BOX(0,50)-(20,70),COLR ELSE IF TILEVIEW(1,2)=0 THEN BOX(0,50)-(60,70),COLR
6280 RETURN
6285 'pareteDX
6290 IF TILEVIEW(2,2)=1 OR TILEVIEW(2,1)=1 THEN GOTO 6303
6295 LINE(120,50)-(95,57),COLR
6300 LINE(120,70)-(95,63),COLR
6302 LINE(95,57)-(95,63),COLR 
6303 IF TILEVIEW(3,3)=1 AND TILEVIEW(3,2)=0 AND TILEVIEW(2,2)=0 THEN BOX(120,50)-(160,70),COLR ELSE IF TILEVIEW(2,2)=1 AND TILEVIEW(3,2)=0 AND TILEVIEW(3,3)=0 THEN BOX(160,50)-(180,70),COLR  ELSE IF TILEVIEW(3,2)=0 THEN BOX(120,50)-(180,70),COLR 
6310 RETURN
6320 '
11100 ' ************
11200 ' * DRAW LAB *
11300 ' ************
11320 ' far,med,close
11330 ' ************
11400 IF TILEVIEW(1,1) = 1 THEN GOSUB 6260
11401 IF TILEVIEW(2,1) = 1 THEN GOSUB 5080
11402 IF TILEVIEW(3,1) = 1 THEN GOSUB 6290
11403 IF TILEVIEW(1,2) = 1 THEN GOSUB 6120
11404 IF TILEVIEW(2,2) = 1 THEN GOSUB 5030
11405 IF TILEVIEW(3,2) = 1 THEN GOSUB 6180
11406 IF TILEVIEW(1,3) = 1 THEN GOSUB 6040
11407 IF TILEVIEW(2,3) = 1 THEN GOSUB 5020
11408 IF TILEVIEW(3,3) = 1 THEN GOSUB 6080
11409 IF BETM(3)=1 THEN BOX(65,50)-(120,100),COLR:IF ETMVL1(ETMS(PLRPSX,PLRPSY,PLRDIR,1))=0 THEN LINE(93,50)-(93,100),COLR 'EXIT DOOR
11415 IF BETM(3)<>2 THEN GOTO 11420
11417 BOXF(70,93)-(110,110),CCHEST:BOXF(89,95)-(91,98),-1:IF BVAL1=0 THEN BOXF(70,85)-(110,90),CCHEST ELSE BOX(72,80)-(108,95),CCHEST 'CHEST
11428 IF BETM(3)=4 THEN PNG$=PLRGR$(3):COLOR 7:GOSUB 49100
11420 '
11500 RETURN
11510 '
11600 ' *************
17000 '  ** PARTY **
17005 ' *************
17007 '
17010 CONSOLE 0,24,0:HPY=LHPY(1):LOCATE 23,HPY,0:COLOR 2:PRINT PLRNM$(1)
17030 COLOR 3:LOCATE 23,HPY+1:PRINT "LV:"
17040 LOCATE 23,HPY+2:PRINT "MP:":LOCATE 30,HPY+2:PRINT "HP:"
17050 COLOR 7:LOCATE 26,HPY+1:PRINT USING"###";PLRLV(1):LOCATE 26,HPY+2:PRINT USING"###";PLRMP(1):LOCATE 33,HPY+2:PRINT USING"###";PLRHP(1)
17060 '****** 
17070 IF HOWMPLR<2 THEN 17120 ELSE CONSOLE 0,24,0:HPY=LHPY(2):LOCATE 23,HPY:COLOR 2:PRINT PLRNM$(2)
17090 COLOR 3:LOCATE 23,HPY+1:PRINT "LV:"
17100 LOCATE 23,HPY+2:PRINT "MP:":LOCATE 30,HPY+2:PRINT "HP:"
17110 COLOR 7:LOCATE 26,HPY+1:PRINT USING"###";PLRLV(2):LOCATE 26,HPY+2:PRINT USING"###";PLRMP(2):LOCATE 33,HPY+2:PRINT USING"###";PLRHP(2)
17120 '****** 
17125 IF HOWMPLR<3 THEN 17510 ELSE CONSOLE 0,24,0:HPY=LHPY(3):LOCATE 23,HPY:COLOR 2:PRINT PLRNM$(3)
17140 COLOR 3:LOCATE 23,HPY+1:PRINT "LV:"
17150 LOCATE 23,HPY+2:PRINT "MP:":LOCATE 30,HPY+2:PRINT "HP:"
17160 COLOR 7:LOCATE 26,HPY+1:PRINT USING"###";PLRLV(3):LOCATE 26,HPY+2:PRINT USING"###";PLRMP(3):LOCATE 33,HPY+2:PRINT USING"###";PLRHP(3)
17170 '****** 
17400 'LOCATE 26,1:PRINT "Dir. "+D$(PLRDIR)
17510 COLOR 7:FOR I=1 TO HOWMPLR:GOSUB 18000:NEXT:GOSUB 18100 'DRAW PLAYERS,GOLD
17550 COLOR CNORM
17580 CONSOLE 16,24,0
17590 LOCATE 0,24,1 'Riaccende cursore
17595 RETURN
' 
18000 'DRAW PLR GRAPHICS
18030 LOCATE 37,LHPY(I),0:PRINT MID$(PLRGR$(I),1,2):LOCATE 37,LHPY(I)+1,0:PRINT MID$(PLRGR$(I),3,2):LOCATE 37,LHPY(I)+2,0:PRINT MID$(PLRGR$(I),5,2)
18040 RETURN
18100 'GOLD
18110 LOCATE 27,1,0:COLOR 3:PRINT "GOLD:":LOCATE 32,1,0:COLOR 7:PRINT USING"####";PLRGLD:COLOR CNORM 
19995 RETURN

      'ITEM MENU, TOWN MANAGEMENT
25000 'TOWN
25010 PLRLOC=0:ISBTL=0:ROOM1=0:BOX(10,15)-(175,60),7:CIRCLE(40,92)30,25,7:BOX(75,70)-(125,115),7:BOX(130,70)-(175,115),7
25020 COLOR 7:LOCATE 8,4:PRINT"DUNGEON":LOCATE 2,11:PRINT"SHRINE":LOCATE 11,11:PRINT"INN":LOCATE 17,11:PRINT"SHOP":LOCATE 26,0,0:PRINT "TOWN SQUARE"
25120 COLOR CNORM:LOCATE 0,24,1:PRINT:PRINT:PRINT"Please select your destination:":PRINT
25130 COLOR CWHITE:PRINT"[1]";:COLOR CNORM:PRINT" Keep calm and enter DUNGEON"
25132 COLOR CWHITE:PRINT"[2]";:COLOR CNORM:PRINT" Cure diseases in the SHRINE"
25134 COLOR CWHITE:PRINT"[3]";:COLOR CNORM:PRINT" Have some rest at the INN"
25136 COLOR CWHITE:PRINT"[4]";:COLOR CNORM:PRINT" Have some business at the SHOP"
25200 K$=INKEY$:RSEED=RND:IF K$="I" THEN PLAY OK$:GOSUB 57000:GOTO 25120 ELSE IF K$="S" THEN PLAY OK$:GOSUB 55300:GOTO 25120
25202 IF K$="" THEN 25200
25205 ISSPMN=0:IF K$="C" THEN PLAY OK$:GOSUB 55200:PRINT:GOTO 25120
25210 KPM=ASC(K$):IF KPM<49 OR KPM>52 THEN 25200
25220 PLAY OK$:KPM=ASC(K$)-48
25250 ON KPM GOSUB 25270,25800,26400,27000
25260 BOXF(0,10)-(180,120),-1:GOTO 25000 
25270 KPM=0:CLS:LOCATE 26,0,0:PRINT SPC(12):BOXF(2,10)-(180,120),-1:GOTO 500'ENTER DUNGEON

'25260 'IF (KPM=1 AND HOWMPLR>1) THEN KPM=0:CLS:LOCATE 26,0,0:PRINT SPC(12):BOXF(2,10)-(180,120),-1:GOTO 500'ENTER DUNGEON
'25270 'IF (KPM=1 AND HOWMPLR<2) THEN KPM=0:GOTO 25000 ELSE KPM=0:BOXF(0,10)-(180,120),-1:GOTO 25000

25300 'DUNGEON
25330 'IF (HOWMPLR<2) THEN COLOR 9:LOCATE 0,24,0:PRINT:PRINT "Better make a party first.":PRINT:PLAY"A0L64PPP":COLOR CNORM
25400 'RETURN
25800 'SHRINE
' DRAW INNER SHRINE
25840 BOXF(0,10)-(180,119),-1:CLS:LOCATE 0,24,0:CSKIN=5:COLR=7:CIRCLEF(87,55),5,CSKIN:BOX(72,63)-(103,90),COLR:BOXF(81,60)-(93,63),COLR
25850 CIRCLE(87,64)10,20,COLR;0,3.1416:LINE(30,90)-(150,90),COLR:LINE(30,90)-(2,95),COLR
25860 LINE(150,90)-(180,95),COLR:BOXF(82,52)-(92,40),COLR:PATTERN "+":PAINT(87,88),COLR
25870 ATTRB 1,1:COLOR COLR:LOCATE 5,3,0:PRINT"SHRINE":ATTRB 0,0:LOCATE 26,0,0:PRINT"  SHRINE   ":COLOR CNORM:LOCATE 0,24:PATTERN 127
25880 PLAY"A0O5L28MISOL84FAL16MIREL84DO":LOCATE 0,24,0:PRINT:PRINT:XTX=0:TX$="May the spirits be with you.":GOSUB 48507:PLAY"A0L32P"
25890 FOR I=1 TO HOWMPLR:PRINT:T=PLRSTS(HOWMPLR,1)+PLRSTS(HOWMPLR,2)+PLRSTS(HOWMPLR,3)
25900 LOCATE 0,24:IF T=0 THEN PRINT:XTX=0:COLOR 2:TX$=PLRNM$(I):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" is fine.":GOSUB 48507:PLAY"A0L32P":GOTO 25960
25910 PRINT:PRINT:LOCATE 0,24,1:XTX=0:COLOR 2:TX$=PLRNM$(I):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" needs to be cured.          I ask for an offer of 5 Golds.":GOSUB 48507
25920 COLOR 7:PRINT"[1]";:COLOR CNORM:PRINT"Accept";:COLOR 7:PRINT"     [2]";:COLOR CNORM:PRINT"Refuse"
25930 K$=INKEY$:RSEED=RND:IF K$="" THEN 25930 ELSE IF K$="1" THEN 25950 ELSE IF K$="2" THEN 25960 ELSE 25930
25950 PLRSTS(HOWMPLR,1)=0:PLRSTS(HOWMPLR,2)=0:PLRSTS(HOWMPLR,3)=0:PRINT:PRINT:XTX=0:COLOR 2:TX$=PLRNM$(I):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$="is now free from diseases.":GOSUB 48507:PLAY"A0L32P"
25960 NEXT:LOCATE 0,24,1:PRINT:PRINT:XTX=0:TX$="Stop by whenever you need my help.":GOSUB 48507:PLAY"A0L32P":LOCATE 0,24,1
26200 K$=INKEY$:RSEED=RND:IF K$="" THEN 26200
26300 RETURN
'
26400 'TAVERN
' DRAW INNER TAVERN
26440 BOXF(0,10)-(180,119),-1:CLS:CSKIN=5:COLR=7:CIRCLEF(87,55),5,CSKIN:BOX(42,63)-(143,90),COLR:BOXF(81,60)-(93,63),15
26450 LINE(30,90)-(150,90),COLR:LINE(30,90)-(2,95),COLR
26460 LINE(150,90)-(180,95),COLR:CIRCLEF(130,80)40,4,15:BOXF(125,85)-(135,110),15
26470 ATTRB 1,1:COLOR COLR:LOCATE 5,3,0:PRINT"TAVERN":ATTRB 0,0:LOCATE 26,0,0:PRINT "  TAVERN   ":COLOR CNORM:LOCATE 0,24:PATTERN 127
26480 IF HOWMPLR>1 THEN GOTO 26610
26490 ATTRB 1,1:LOCATE 4,8:COLOR 7:PRINT MID$(PLRGR$(2),1,2):LOCATE 4,10:PRINT MID$(PLRGR$(2),3,2):LOCATE 4,12:PRINT MID$(PLRGR$(2),5,2):ATTRB 0,0
26500 PLAY"A1O4L8MIFALA":COLOR CNORM:LOCATE 0,24:XTX=0:TX$="Entering the tavern you see a big guy   approaching you.":GOSUB 48507:PLAY"A0L32PP"
26510 LOCATE 0,24:PRINT:PRINT:XTX=0:TX$=CHR$(34)+"My name is MOLUOC. I'm a cleric.        You seem looking for a quest.":GOSUB 48507:PLAY"A0L32P"
26520 PRINT:XTX=0:TX$=" Me and my friend DUPLEX the mage were   wandering in the dungeon.":GOSUB 48507:PLAY"A0L32P"
26530 PRINT:XTX=0:TX$=" I lost him while fighting a golem.      I need a party as you do.":GOSUB 48507:PLAY"A0L32P"
26540 PRINT:XTX=0:TX$=" Please let me join to rescue him,       we'll be happy to share our loots!"+CHR$(34):GOSUB 48507:PLAY"A0L32PP"
26550 PRINT:PRINT:PRINT:XTX=0:TX$="You accept."+CHR$(13)+CHR$(10):GOSUB 48507:PLAY"A0L32P"
26560 PRINT:PRINT:PRINT:XTX=0:COLOR 2:TX$="MOLUOC":GOSUB 48507::XTX=XTX+1:COLOR CNORM:TX$=" JOINS THE PARTY !!":GOSUB 48507
26570 HOWMPLR=2:PLRGLD=PLRGLD+10:GOSUB 17000:SCREEN,,2:PLAY"A1L16O4SIPSISISIL16PO5DO#PO5L06RE#DO#RE#DO#RE#DO#RE#DO#RE#DO#RE#DO#RE#":SCREEN,,0:GOTO 26670
26600 '*** TAVERN MENU
26610 COLOR CNORM:LOCATE 0,24,1:PRINT:PRINT:PRINT"Please select what to do here:":PRINT
26620 COLOR CWHITE:PRINT"[1] ";:COLOR CNORM:PRINT"Get a room and rest (";10*HOWMPLR;"Gold )"
26625 COLOR CWHITE:PRINT"[2] ";:COLOR CNORM:PRINT"Beer and chuckle around (";1*HOWMPLR;"Gold )"
26630 COLOR CWHITE:PRINT"[3] ";:COLOR CNORM:PRINT"Back to town square"
26640 K$=INKEY$:RSEED=RND:IF K$="" THEN 26640
'26645 ISSPMN=0:IF K$="C" THEN PLAY OK$:GOSUB 55200:PRINT:GOTO 26610
26650 KPA=ASC(K$)-48:IF KPA<1 OR KPA>3 THEN 26640 ELSE IF KPA=3 THEN PLAY OK$:RETURN
26660 PLAY OK$:IF KPA=1 THEN 26680 ELSE 26735
26670 BOXF(0,10)-(180,119),-1:GOTO 26400
26680 IF (PLRGLD<10*HOWMPLR) THEN PLAY"A0O3L18DO":GOTO 26640
26700 PLRGLD=PLRGLD-(10*HOWMPLR):GOSUB 18100:BOXF(0,10)-(179,119),-1:FOR Y=10 TO 30:PSET(RND*(140)+Y,RND*(80)+Y),6:NEXT:CIRCLEF(50,40),20,3:CIRCLEF(55,40),15,-1
26710 COLOR CNORM:LOCATE 0,24,0:PRINT:PRINT "Starry night ...":FOR I=1 TO 4:PLAY"A5O5L3SISISISIL8PPPL3SISISISISISIL64P" :NEXT
26720 FOR I=1 TO HOWMPLR:PLRMP(I)=PLRMXMP(I):PLRHP(I)=PLRMXHP(I):NEXT:GOSUB 17000:PRINT:PRINT"PARTY feels RESTORED !!":PRINT 'RESTORE PLAYERS
26730 GOTO 26670
26735 IF (PLRGLD<3*HOWMPLR) THEN PLAY"A0O3L18DO":GOTO 26640 ELSE PLRGLD=PLRGLD-(3*HOWMPLR):GOSUB 18100:LOCATE 0,24,0
26740 SNG$(1)="MANOWAR's 'Hearing the call'.":SNG$(2)="ANGRA's 'Nothing to say'.":SNG$(3)="SYMPHONY X's 'Inferno'.":SNG$(4)="BRUCE DICKINSON's 'Tears of a dragon'.":SNG$(5)="HELLOWEEN's 'Future World'."
26745 TVR$(1)=" elf ":TVR$(2)=" dwarf ":TVR$(3)=" mage ":TGM$(1)="'VAMPIRE' on THOMSON MO5":TGM$(2)="'L'AFFAIRE VERA CRUZ' on THOMSON TO7":TGM$(3)="'LES DIEUX DE LA MER' on THOMSON MO6"
26747 TGM$(4)="'L'EREDITA' on PC128 OLIVETTI PRODEST":TGM$(5)="'LE 5EME AXE' on THOMSON MO5":TGM$(6)="'CAPTAIN BLOOD' on THOMSON MO6"
26750 QST$(1)="MOLUOC informs you that the golem was"+CHR$(13)+CHR$(10)+"9 steps N, 10 steps E, 5 steps N."
26760 PRINT:PRINT"Folks of all races giggle and drink.":PRINT"BART THE BARD sits on a table singing":PRINT SNG$(INT(RND*(4)+1))
26770 PRINT"A"+TVR$(INT(RND*(3)+1))+"is having fun playing"+CHR$(13)+CHR$(10)+TGM$(INT(RND*(6)+1))
26775 K$=INKEY$:RSEED=RND:IF K$="" THEN 26775 ELSE PRINT:PRINT QST$(1)
26780 K$=INKEY$:RSEED=RND:IF K$="" THEN 26780 ELSE GOTO 26610
27000 'SHOP
' DRAW INNER SHOP
27050 BOXF(0,10)-(180,119),-1:CLS:CSKIN=5:COLR=7:BOX(30,35)-(65,90),15:BOX(70,35)-(110,90),15:BOX(115,35)-(150,90),15
27060 BOXF(42,63)-(143,90),-1
27070 CIRCLEF(87,55),5,CSKIN:BOX(42,63)-(143,90),COLR:BOXF(81,60)-(93,63),2
27080 LINE(30,90)-(150,90),COLR:LINE(30,90)-(2,95),COLR
27090 LINE(150,90)-(180,95),COLR
27100 ATTRB 1,1:COLOR COLR:LOCATE 7,3,0:PRINT"SHOP":ATTRB 0,0:LOCATE 26,0,0:PRINT "    SHOP   ":COLOR CNORM:LOCATE 0,24:PATTERN 127:PLAY"A1L84O4LAFA"
27120 LOCATE 0,24,1:PRINT:XTX=0:TX$="How can i serve you?":GOSUB 48507:LOCATE 0,24,1:PRINT
27130 COLOR CWHITE:PRINT"[1] ";:COLOR CNORM:PRINT"Buy":COLOR CWHITE:PRINT"[2] ";:COLOR CNORM:PRINT"Sell":COLOR CWHITE:PRINT"[3] ";:COLOR CNORM:PRINT"Exit"
27150 K$=INKEY$:RSEED=RND:IF K$="" THEN 27150 ELSE IF K$="1" THEN 27300 ELSE IF K$="2" THEN 27160 ELSE IF K$="3" THEN PLAY OK$:BOXF(0,10)-(180,119),-1:RETURN ELSE 27150 
27160 PLAY OK$:PMIN=1:PMAX=HOWMPLR:PRINT:PRINT"Please tell me who wants to SELL:":PRINT:GOSUB 56000:PLAY OK$:PRINT:PRINT"Please select the item you would sell:":PRINT:PLAY"A0O1L32P" 'SELL
27170 IC=0:FOR I=1 TO 7:EQ$="":IF INVNT(I,PRT)=0 THEN 27180 ELSE IC=IC+1:ICS(IC)=ITMID(INVNT(I,PRT)):IF ICS(IC)=EQUIP(1,PRT) OR ICS(IC)=EQUIP(2,PRT) OR ICS(IC)=EQUIP(3,PRT) THEN EQ$="(E)"
27175 PRINT STR$(IC)+") "+ITMNM$(INVNT(I,PRT))+EQ$ 'GET INVENTORY FOR PLRINV(PRT)=ID OBJ
27180 NEXT: IF IC=0 THEN PRINT:XTX=0:TX$="... No items? Ok then ...":GOSUB 48500:GOTO 27120
27190 K$=INKEY$:RSEED=RND:IF K$="" THEN 27190 ELSE KPA=ASC(K$)-48:IF KPA<1 OR KPA>IC THEN 27190
27200 PLAY OK$:IF ITMPRC(ICS(KPA))=0 THEN PRINT:PRINT"... Hmmm, i can't accept this.":PLAY "A0O1L32PPP":GOSUB 48500:GOTO 27120    'IF HE CAN ACCEPT THE ITEM
27210 PLAY OK$:PRINT:PRINT"... Hmmm, for this i can offer:":PRINT:COLOR 7:PRINT STR$(INT(ITMPRC(ICS(KPA))/2));:COLOR CNORM:PRINT"Gold" 'SET PRICE 
27220 PRINT:COLOR 7:PRINT"[1]";:COLOR CNORM:PRINT"Accept      ";:COLOR 7:PRINT"[2]";:COLOR CNORM:PRINT"Refuse"
27230 K$=INKEY$:RSEED=RND:IF K$="" OR (K$<>CHR$(49) AND K$<>CHR$(50)) THEN 27230
27240 PLAY OK$:IF K$=CHR$(50) THEN PRINT:PRINT:XTX=0:TX$="Nevermind, sir ...":GOSUB 48500:GOTO 27120 
27245 PLRGLD=PLRGLD+INT(ITMPRC(ICS(KPA))/2):GOSUB 18100:PLAY"A1O5L06SOLASI":LOCATE 0,24,0:PRINT:PRINT:XTX=0:TX$="Here's your gold, sir. Thanks!":GOSUB 48500:PRINT
27248 FOR I=1 TO 3:IF EQUIP(I,PRT)=ICS(KPA) THEN EQUIP(I,PRT)=0
27250 NEXT:INVNT(KPA,PRT)=0:GOTO 27120
27300 PLAY OK$:PRINT:PRINT"Please choose what to BUY from:":PRINT 'BUY
27302 COLOR CWHITE:PRINT"[1]";:COLOR CNORM:PRINT" WEAPONS"
27304 COLOR CWHITE:PRINT"[2]";:COLOR CNORM:PRINT" SHIELDS"
27306 COLOR CWHITE:PRINT"[3]";:COLOR CNORM:PRINT" ARMORS"
27308 COLOR CWHITE:PRINT"[4]";:COLOR CNORM:PRINT" ALCHEMY/OTHER"
27309 K$=INKEY$:RSEED=RND:IF K$="" THEN 27309
27310 KPA=ASC(K$):IF KPA<49 OR KPA>52 THEN 27309 ELSE PLAY OK$:KPA=ASC(K$)-48
27315 PRINT:PRINT"Ok, and for which player specifically?":PRINT:PMIN=1:PMAX=HOWMPLR:GOSUB 56000:PRC=PRT:PLAY OK$
27320 T=0:FOR I=1 TO 7:IF INVNT(I,PRT)=0 THEN EXIT ELSE T=T+1:NEXT
27325 IF T=7 THEN COLOR 9:PRINT:PRINT PLRNM$(PRT)+"'s inventory is full!":PLAY"A0O1L32PP":PRINT:PRINT:GOTO 27120
27330 PRINT:PRINT"Take a look and choose (B for BACK):":PRINT:PRC=0:IF KPA<4 THEN PRC=PRT
27335 T=0:FOR I=1 TO TTITMS:IF ITMKND(I)=KPA AND ITMRC(I)=PRC THEN 27337 ELSE 27340
27337 T=T+1:ITMSC(T)=ITMID(I):COLOR 7:PRINT STR$(T)+")";:COLOR 2:PRINT USING"%            %";ITMNM$(I);:COLOR CNORM
27338 PRINT USING"%            %";ITMDSC$(I);:COLOR 7:PRINT USING"####";ITMPRC(I);:COLOR CNORM:PRINT"Gold"
27340 NEXT
27345 K$=INKEY$:RSEED=RND:IF K$="" THEN 27345 ELSE KPA=ASC(K$):IF KPA=66 THEN 27500
27350 KPA=ASC(K$)-48:IF KPA<1 OR KPA>T THEN 27345
27355 IF PLRGLD<ITMPRC(ITMSC(KPA)) THEN PLAY"A0O3L18DO":GOTO 27345 ELSE PLAY OK$
      'you can't buy a weapon/shield/armor which you already have
27360 FOR I=1 TO 7:IF INVNT(I,PRT)=ITMID(ITMSC(KPA)) AND ITMONC(ITMSC(KPA))=0 THEN EXIT ELSE NEXT
27363 IF I<7 THEN COLOR 9:PRINT:PRINT PLRNM$(PRT)+" already owns this.":PLAY"A0O1L32PP":PRINT:PRINT:COLOR CNORM:GOTO 27120 ELSE FOR I=1 TO 7:IF INVNT(I,PRT)=0 THEN EXIT ELSE NEXT  
27365 PRINT:PRINT"Thank you sir! Nice Purchase!":PLAY"A1O5L06SOLASI":PLAY"A0O1L32PPP":INVNT(I,PRT)=ITMID(ITMSC(KPA)):PLRGLD=PLRGLD-ITMPRC(ITMSC(KPA)):GOSUB 18100:LOCATE 0,24,0:GOTO 27120
27500 PLAY OK$:PRINT:PRINT:XTX=0:TX$="A change of mind eh? Well ...":GOSUB 48500:GOTO 27120 ELSE IF KPA<49 OR KPA>52 THEN 27335 ELSE PLAY OK$:KPA=ASC(K$)-48


30000 '
      '...... GAME SETUP  .....
      'PARTY AND PLAYER DATA
	  'Name,Level,XP,NextXP,HP,MaxHP,MP,MaxMP,AP,DP,
	  'RaceClass,Speed,Luck,Critical,MaGicPower
	  '
30001 PRINT "Building characters ..."	  
30010 DEFGR$(0)=24,36,36,36,24,60,24,24:ANK$=GR$(0):LHPY(1)=3:LHPY(2)=7:LHPY(3)=11:NORTH=1:EAST=2:SOUTH=3:WEST=4:CNTR=0:CURDIR=0:DRAW=0:DIM D$(4):DIM TILEVIEW(3,3):A$="":OK$="A0L4O3LAO5LA"
30015 D$(NORTH)="NORTH":D$(SOUTH)="SOUTH ":D$(EAST)="EAST ":D$(WEST)="WEST ":RACE$(1)="PALADIN":RACE$(2)="CLERIC":RACE$(3)="MAGE":PSONED=1:PRLYZED=2:CNFSED=3
30020 STS$(0)="FINE":STS$(PSONED)="POISONED":STS$(PRLYZED)="PARALYZED":STS$(CNFSED)="CONFUSED"
30030 DIM PLRNM$(3):DIM PLRLV(3):DIM PLRXP(3):DIM PLRNXP(3)
30040 DIM PLRHP(3):DIM PLRMXHP(3):DIM PLRMP(3):DIM PLRMXMP(3)
30050 DIM PLRAP(3):DIM PLRDP(3):DIM PLRRC(3):DIM PLRSP(3)
30060 DIM PLRLP(3):DIM PLRCP(3):DIM PLRMGP(3)
      'equip: Weapon, Shield, Armor; Attack, Defense, Speed
	  'bonus: Attack, Defend, Speed, CriticalP
30070 DIM INVNT(7,3):DIM EQUIP(3,3):DIM PLRBNS(4,3):FOR I=1 TO 3:FOR T=1 TO 7:INVNT(T,I)=0:NEXT:FOR T=1 TO 3:EQUIP(T,I)=0:NEXT:FOR T=1 TO 4:PLRBNS(T,I)=0:NEXT:NEXT         
30090 DIM PLRSTS(3,3) 'POISONED,PARALYZED,CONFUSED
30100 PLRGLD=50:HOWMPLR=1:FCHN=10 'gold,how in party,Fighting CHaNce
      'Init Party
	  'Name,Level,XP,NextXP,HP,MaxHP,MP,MaxMP,AP,DP,
	  'RaceClass,Speed,Luck,Critical,MaGicPower	  
30110 DATA "SIOBENNEH"   ,1,0,10,12,10,0,0,5,3,1,1,2,1,0
30120 DATA "MOLUOC"      ,1,0,12,8,8,10,8,2,3,2,2,5,0,1
30130 DATA "DUPLEX"      ,3,10,24,6,10,10,10,1,1,5,6,4,0,2
30140 FOR X=1 TO 3
30150 READ PLRNM$(X):READ PLRLV(X):READ PLRXP(X):READ PLRNXP(X)
30160 READ PLRHP(X):READ PLRMXHP(X):READ PLRMP(X):READ PLRMXMP(X)
30170 READ PLRAP(X):READ PLRDP(X):READPLRRC(X):READ PLRSP(X)
30180 READ PLRLP(X):READ PLRCP(X):READ PLRMGP(X)
      'STATUS(PLR,sts) '1=POISONED,2=PARALYZED,3=CONFUSED:val 0=NO,1=YES  
30190 PLRSTS(X,PSONED)=0:PLRSTS(X,PRLYZED)=0:PLRSTS(X,CNFSED)=0
30200 NEXT

'X,Y,DIR ON LABYRINTH MAP
30205 DIM COORDX(9,9,4):DIM COORDY(9,9,4):DIM MOVF(2,4):DIM MOVB(2,4)
'NORTH
30210 COORDX(1,1,NORTH)=-1:COORDX(3,1,NORTH)=1:COORDX(1,2,NORTH)=-1:COORDX(3,2,NORTH)=1:COORDX(1,3,NORTH)=-1:COORDX(3,3,NORTH)=1
30220 COORDY(1,1,NORTH)=-2:COORDY(2,1,NORTH)=-2:COORDY(3,1,NORTH)=-2:COORDY(1,2,NORTH)=-1:COORDY(2,2,NORTH)=-1:COORDY(3,2,NORTH)=-1
'EAST
30230 COORDX(1,1,EAST)=2:COORDX(2,1,EAST)=2:COORDX(3,1,EAST)=2:COORDX(1,2,EAST)=1:COORDX(2,2,EAST)=1:COORDX(3,2,EAST)=1
30240 COORDY(1,1,EAST)=-1:COORDY(3,1,EAST)=1:COORDY(1,2,EAST)=-1:COORDY(3,2,EAST)=1:COORDY(1,3,EAST)=-1:COORDY(3,3,EAST)=1
30250 END
'SOUTH
30250 COORDX(1,1,SOUTH)=1:COORDX(3,1,SOUTH)=-1:COORDX(1,2,SOUTH)=1:COORDX(3,2,SOUTH)=-1:COORDX(1,3,SOUTH)=1:COORDX(3,3,SOUTH)=-1
30260 COORDY(1,1,SOUTH)=2:COORDY(2,1,SOUTH)=2:COORDY(3,1,SOUTH)=2:COORDY(1,2,SOUTH)=1:COORDY(2,2,SOUTH)=1:COORDY(3,2,SOUTH)=1
'WEST
30270 COORDX(1,1,WEST)=-2:COORDX(2,1,WEST)=-2:COORDX(3,1,WEST)=-2:COORDX(1,2,WEST)=-1:COORDX(2,2,WEST)=-1:COORDX(3,2,WEST)=-1
30280 COORDY(1,1,WEST)=1:COORDY(3,1,WEST)=-1:COORDY(1,2,WEST)=1:COORDY(3,2,WEST)=-1:COORDY(1,3,WEST)=1:COORDY(3,3,WEST)=-1
'
'MOVEMENTS ON GENERAL MAP
'1 = X, 2 = Y
30290 MOVF(2,NORTH)=-1:MOVB(2,NORTH)=1:MOVF(2,SOUTH)=1:MOVB(2,SOUTH)=-1:MOVF(1,EAST)=1:MOVB(1,EAST)=-1:MOVF(1,WEST)=-1:MOVB(1,WEST)=1

30296 PRINT "Building items ..."

	  'MUSIC DATA
30300 DATA "RE","P","RE","RE","RE","P","RE","RE","RE","P","RE","RE","LA","SI","RE","P"
30310 DIM MUSIC$(16):FOR I=1 TO 16:READ MUSIC$(I):NEXT:CBGMN=1 'Current note
      
	  'ITEMS
	  'ID,
	  'NAME,DESC,LEVEL,PRICE,KIND,VAL,EFFECT,RACE,CRITICAL,MAGIC
	  'KIND:1=WEAP,2SHIELD,3=ARMOR,4=OTHER
	  'EFFECT:DAMAGE,SETSTATUS,HEAL,EXIT,OPEN DOOR,MAP,ecc...
      'RACE:0=ALL
      'MAGIC:CAST SPECIFIC MAGIC 	
	  'ITMULC 0=everywhere,anytime(herb) 1=only external 2=only dungeon,no battle(map) 3=only dungeon,battle(weapons,shields,ecc.), 4=NEVER
	  'ITMONC 0=not remove when used 1=removed when used
30320 TTITMS=31:DIM ITMID(TTITMS):DIM ITMNM$(TTITMS):DIM ITMDSC$(TTITMS):DIM ITMLV(TTITMS):DIM ITMPRC(TTITMS):DIM ITMKND(TTITMS)
30325 DIM ITMVAL(TTITMS):DIM ITMFX(TTITMS):DIM ITMRC(TTITMS):DIM ITMCP(TTITMS):DIM ITMMGC(TTITMS):DIM ITMULC(TTITMS):DIM ITMONC(TTITMS)
30330 DATA "HERB","Heal",0,25,4,25,3,0,0,0,0,1:DATA "ANTIDOTE","Cure poison",0,15,4,25,3,0,0,0,0,1:DATA "SALT","Deparalyze",0,20,4,25,3,0,0,0,0,1:DATA "FLASH","Exit dungeon",0,10,4,25,3,0,0,0,3,1:DATA "MAP1","Floor1 Map",0,10,4,1,6,0,0,0,2,0
30340 DATA "SABER SWORD","DMG+2",1,75,1,25,3,1,0,0,2,0:DATA "STEEL SWORD","DMG+4",4,125,1,25,3,1,0,0,2,0:DATA "MITHRIL SWORD","DMG+8",12,310,1,25,3,1,0,0,2,0:DATA "VIBRANT SWORD","DMG+16",30,650,1,25,3,1,0,0,2,0
30342 DATA "SABER MACE","DMG+2",1,75,1,25,3,2,0,0,2,0:DATA "STEEL MACE","DMG+4",4,125,1,25,3,2,0,0,2,0:DATA "MITHRIL MACE","DMG+8",12,310,1,25,3,2,0,0,2,0:DATA "VIBRANT MACE","DMG+16",30,650,1,25,3,2,0,0,2,0
30346 DATA "SABER STAFF","DMG+2",1,75,1,25,3,3,0,0,2,0:DATA "STEEL STAFF","DMG+4",4,125,1,25,3,3,0,0,2,0:DATA "MITHRIL STAFF","DMG+8",12,310,1,25,3,3,0,0,2,0:DATA "VIBRANT STAFF","DMG+16",30,650,1,25,3,3,0,0,2,0
30348 DATA "STEEL SHIELD","DMG+2",1,75,2,25,3,1,0,0,2,0:DATA "CROSS SHIELD","DMG+4",4,125,2,25,3,2,0,0,2,0:DATA "MITHRIL SHIELD","DMG+8",12,310,2,25,3,2,0,0,2,0:DATA "VIBRANT SHIELD","DMG+16",30,650,2,25,3,1,0,0,2,0
30350 DATA "JADE SCARF","DMG+2",1,75,2,25,3,3,0,0,2,0:DATA "MIRROR ORB","DMG+4",4,125,2,25,3,3,0,0,2,0
30352 DATA "SABER ARMOR","DMG+2",1,75,3,25,3,1,0,0,2,0:DATA "STEEL ARMOR","DMG+4",4,125,3,25,3,1,0,0,2,0:DATA "MITHRIL ARMOR","DMG+8",12,310,3,25,3,1,0,0,2,0:DATA "VIBRANT ARMOR","DMG+16",30,650,3,25,3,1,0,0,2,0
30354 DATA "CHAIN MADE","DMG+2",1,75,3,25,3,2,0,0,2,0:DATA "CLERICAL","DMG+4",4,125,3,25,3,2,0,0,2,0:DATA "MAGE CAPE","DMG+8",12,310,3,25,3,3,0,0,2,0:DATA "MENTOR CAPE","DMG+16",30,650,3,25,3,3,0,0,2,0

30496 FOR I=1 TO TTITMS:ITMID(I)=I:READ ITMNM$(I):READ ITMDSC$(I):READ ITMLV(I):READ ITMPRC(I):READ ITMKND(I)
30498 READ ITMVAL(I):READ ITMFX(I):READ ITMRC(I):READ ITMCP(I):READ ITMMGC(I):READ ITMULC(I):READ ITMONC(I):NEXT

	  '25 SPELLS
'HEAL1,BOLT1,SLEEP,FIRE1,DEPOISON,EXIT,ICE1,CONFUSION,SLOW
'VISION,PEACE1,BOLT2,DEPARAL,FIRE2,HEAL2,ICE2,BOLT2,PEACE2,BOLT3
'REVIVE,FIRE3,HEAL3,ICE3,PEACE3,DESOUL

'NAME,LV,MP,FXKND,FXVAL,TargetSPEC,SCRCLR1,SCRCLR2,STR2PLAY
30500 TTSPLLS=14
30510 HEAL=1:BOLT=2:SLEEP=3:FIRE=4:DEPSN=5:EXXT=6:ICE=7:CONFSN=8:SLOW=9
30510 VSION=10:PEACE=11:DEPARAL=12:REVIVE=13:DESOUL=14
30540 DIM MGCNM$(TTSPLLS):DIM MGCLV(TTSPLLS):DIM MGCMXLV(TTSPLLS):DIM MGCMP(TTSPLLS)
30550 DIM MGCKND(TTSPLLS):DIM MGCVAL(TTSPLLS):DIM MGCTSPEC(TTSPLLS) '1..8=MONSTERS, 9=ALL MONST, 10,11,12=PLR1,2,3
30560 DIM MGCSC1(TTSPLLS):DIM MGCSC2(TTSPLLS):DIM MGCSND$(TTSPLLS)
30580 DATA "HEAL",1,3,4,3,20,10,2,0,"L2O4SI":DATA "BOLT",1,3,3,1,8,1,3,15,"A2L1O5SI"
30590 DATA "SLEEP",11,1,11,2,11,1,8,11,"A1L2O5LA":DATA "FIRE",11,3,12,1,11,1,15,1,"A1L2O3LA"
30600 DATA "DEPOISON",11,2,11,2,11,1,3,0,"A1L2O5SI":DATA "EXIT",11,1,11,13,11,11,3,6,"A1L2O5LA"
30610 DATA "ICE",11,3,11,1,11,11,4,14,"A1L2O5SI":DATA "CONFUSION",11,2,11,1,11,11,8,11,"A1L2O5LA"
30620 DATA "SLOW",11,2,11,1,11,11,8,11,"A1L2O5LA":DATA "VISION",11,3,11,13,11,11,3,6,"A1L2O5LA"
30630 DATA "PEACE",11,3,11,13,11,11,3,6,"A1L2O5LA":DATA "DEPARAL",11,1,11,13,11,11,3,0,"A1L2O5LA"
30670 DATA "REVIVE",11,2,11,13,11,11,3,2,"A1L2O5SI":DATA "DESOUL",11,2,11,1,11,11,13,0,"A1L2O5LA"
30700 FOR X=1 TO TTSPLLS:READ MGCNM$(X):READ MGCLV(X):READ MGCMXLV(X):READ MGCMP(X)
30710 READ MGCKND(X):READ MGCVAL(X):READ MGCTSPEC(X):READ MGCSC1(X):READ MGCSC2(X)
30720 READ MGCSND$(X):NEXT  
31000 DIM PLRMGS(2,7):DIM MGCSCR(2,7):DIM MGCSPW(2,7):DIM MGCHS(2)  'PLAYER MAGICS (PLRID-1, 0 has no magic)	 
      'MOLOUC SPELLS
31010 DATA 1,2,5,6,7,9,13:FOR X=1 TO 7:READ PLRMGS(1,X):NEXT
      'DUPLEX SPELLS
31020 DATA 1,2,4,6,7,8,10:FOR X=1 TO 7:READ PLRMGS(2,X):NEXT
	  
	  'EFFECTS KIND:
	  'DAMAGE,SETSTATUS,HEAL,REVIVE
		  
      'GRAPHICS SETTINGS AND DATA
	  'SIOBENNEH
31150 DEFGR$(100)=3,7,7,4,6,6,2,25
31160 DEFGR$(101)=192,224,224,32,96,100,72,148
31170 DEFGR$(102)=60,54,108,97,106,100,90,88
31180 DEFGR$(103)=46,90,160,95,83,215,221,14
31190 DEFGR$(104)=99,78,78,92,92,64,92,189
31200 DEFGR$(105)=224,114,114,58,58,2,58,189
      'MOLUOC
31230 DEFGR$(106)=1,2,3,0,3,2,11,29
31240 DEFGR$(107)=131,197,198,8,208,96,208,184
31250 DEFGR$(108)=19,63,117,106,55,51,69,133
31260 DEFGR$(109)=76,190,174,70,236,204,160,160
31270 DEFGR$(110)=13,13,29,30,29,12,8,56
31280 DEFGR$(111)=176,176,184,120,184,48,16,28
	  'DUPLEX
31290 DEFGR$(112)=3,6,4,14,12,12,30,27
31300 DEFGR$(113)=128,64,38,105,37,33,114,84
31310 DEFGR$(114)=61,126,123,247,238,245,242,101
31320 DEFGR$(115)=168,16,40,72,168,88,92,88
31330 DEFGR$(116)=88,91,35,43,159,59,118,172
31340 DEFGR$(117)=118,86,104,116,88,108,54,27

31500 DIM PLRGR$(3):PLRGR$(1)=GR$(100)+GR$(101)+GR$(102)+GR$(103)+GR$(104)+GR$(105)
31502 PLRGR$(2)=GR$(106)+GR$(107)+GR$(108)+GR$(109)+GR$(110)+GR$(111)
31505 PLRGR$(3)=GR$(112)+GR$(113)+GR$(114)+GR$(115)+GR$(116)+GR$(117)	  
	  
31506 PRINT "Building enemies ..."
	  
32010 '..... Colors
32020 CNORM=3:CFIRE=1:CHIT=1:CNAME=2:CCHEST=15:CSNOW=6:CWHITE=7
32025 CWALL=8:CSEL=11:COTHER=9
33030 '..... Monsters
33040 HIMP=1:INFOGRAM=2:MANDRD=3:BADFLWR=4:SLIME=5:SKEL=6:GLEM=7
'
34000 'HIMP
34010 DEFGR$(10)=36,66,129,219,126,90,36,66
34020 DEFGR$(11)=102,60,66,90,102,90,36,102
34030 'INFOGRAM
34040 DEFGR$(12)=0,0,7,24,32,67,75,107
34050 DEFGR$(13)=0,0,128,64,0,128,160,168
34060 DEFGR$(14)=109,117,53,26,29,48,0,0
34070 DEFGR$(15)=108,90,94,176,96,56,0,0
34080 'ANDROID (MANDRD)
34090 DEFGR$(16)=16,56,84,56,16,16,16,16
34100 'BADFLOWER
34110 DEFGR$(17)=0,108,254,186,146,252,42,2
34120 DEFGR$(18)=68,106,48,20,40,8,50,252
34130 'SLIME/BLOB
34140 DEFGR$(19)=0,3,12,27,55,63,51,57
34150 DEFGR$(20)=0,192,176,248,252,252,204,156
34160 DEFGR$(21)=117,113,47,124,123,63,15,0
34170 DEFGR$(22)=174,142,244,62,222,252,240,0
34500 'SKELETON
34510 DEFGR$(23)=7,12,27,15,19,81,73,78
34520 DEFGR$(24)=224,240,248,240,200,136,144,112
34530 DEFGR$(25)=71,64,71,65,70,79,89,179
34540 DEFGR$(26)=224,0,224,128,110,219,151,215
34550 DEFGR$(27)=225,67,2,6,4,12,24,120
34560 DEFGR$(28)=151,223,78,96,32,48,24,30
34570 'GOLEM
34580 DEFGR$(30)=0,0,1,1,1,0,1,1
34590 DEFGR$(31)=124,238,85,239,57,16,57,215
34600 DEFGR$(32)=0,0,0,0,0,0,0,0
34610 DEFGR$(33)=1,10,23,47,39,44,38,46
34620 DEFGR$(34)=125,214,124,131,127,60,191,188
34630 DEFGR$(35)=0,128,224,220,186,111,71,87
34640 DEFGR$(36)=54,123,24,107,227,211,107,5
34650 DEFGR$(37)=31,126,153,2,125,123,247,237
34660 DEFGR$(38)=71,111,61,93,170,212,104,240
34670 DEFGR$(39)=2,5,14,30,14,54,121,29
34680 DEFGR$(40)=219,23,174,119,251,120,51,5
34690 DEFGR$(41)=240,226,193,177,113,209,209,179
34700 DEFGR$(42)=93,93,95,93,95,71,51,122
34710 DEFGR$(43)=2,5,6,7,3,3,1,0
34720 DEFGR$(44)=227,7,141,91,175,23,10,4
'
37000 '
37010 '
37020 '******** MONSTERS DATA
37010 TTENM=7 '**** total enemies
'
37020 DIM ENMGR$(TTENM)
37030 ENMGR$(HIMP)=GR$(10)+GR$(11)
37040 ENMGR$(INFOGRAM)=GR$(12)+GR$(13)+GR$(14)+GR$(15)
37050 ENMGR$(MANDRD)=GR$(16):ENMGR$(BADFLWR)=GR$(17)+GR$(18)
37060 ENMGR$(SLIME)=GR$(19)+GR$(20)+GR$(21)+GR$(22)
37070 ENMGR$(SKEL)=GR$(23)+GR$(24)+GR$(25)+GR$(26)+GR$(27)+GR$(28) 
37080 ENMGR$(GLEM)=GR$(30)+GR$(31)+GR$(32)+GR$(33)+GR$(34)+GR$(35)+GR$(36)+GR$(37)+GR$(38)+GR$(39)+GR$(40)+GR$(41)+GR$(42)+GR$(43)+GR$(44) 
'
38020 DIM ENMNM$(TTENM) 'Name, 
38030 DIM ENMCL(TTENM)  'Color
38040 DIM ENMGW(TTENM):DIM ENMGH(TTENM) 'SpriteWidth, SpriteHeight
38050 DIM ENMHP(TTENM):DIM ENMMP(TTENM) 'Health, Mana
38060 DIM ENMAP(TTENM):DIM ENMDP(TTENM) 'Attack, Defense
38070 DIM ENMCP(TTENM):DIM ENMWP(TTENM) 'C.Hit factor, %magic use
38080 DIM ENMSP(TTENM):DIM ENMGD(TTENM) 'Speed, Gold
38090 DIM ENMXP(TTENM):DIM ENMLV(TTENM)	'XP,Level
'magics, statusattacK(%posioning,%paralyzing,%callsforhelp)
38100 DIM ENMMG(2,TTENM):DIM ENMSTK(3,TTENM)	
' 
'
' Name, color, width, height, level, health, mana, 
' attack, defense, c.hit, mag.use, speed, gold, XP
' magic1, magic2, %posioning, %paralyzing, %callsforhelp

39000 DATA "HIMP"      ,5,1,2,1,8,0,2,1,0,0,2,2,2,0,0,0,0,0
39010 DATA "INFOGRAM"  ,7,2,2,1,7,0,2,2,0,0,1,3,3,0,0,0,0,0
39020 DATA "ANDROID"   ,3,1,1,1,6,0,1,2,0,0,8,1,1,0,0,0,0,0
39030 DATA "BADFLOWER" ,10,1,2,2,12,0,3,2,0,0,4,3,1,0,0,0,0,0
39040 DATA "SLIME"     ,2,2,2,2,15,0,4,1,0,0,1,2,2,0,0,0,0,0
39100 DATA "SKELETON"  ,7,2,3,2,17,0,6,2,1,0,4,4,3,0,0,0,0,0
39110 DATA "GOLEM"     ,14,3,5,3,40,0,12,2,1,0,1,20,15,0,0,0,0,0

'
40000 FOR X=1 TO TTENM
40050 READ ENMNM$(X):READ ENMCL(X):READ ENMGW(X):READ ENMGH(X)
40055 READ ENMLV(X):READ ENMHP(X):READ ENMMP(X):READ ENMAP(X)
40060 READ ENMDP(X):READ ENMCP(X):READ ENMWP(X):READ ENMSP(X)
40070 READ ENMGD(X):READ ENMXP(X)
40080 READ ENMMG(1,X):READ ENMMG(2,X)
40090 READ ENMSTK(1,X):READ ENMSTK(2,X):READ ENMSTK(3,X)
40100 NEXT 
40150 DIM ENMSCR(7):DIM WARCH(3,3)      'Enemies on screen
40160 DIM ENMSHP(7):DIM ENMSST(7)       'HP,Status enemies on screen
40165 DIM ENMSLX(7):DIM ENMSLY(7)       'Locate x,y enemies on screen
40170 DIM ENMSSP(7):DIM TURNISP(10)     'Speed enemies on screen, turn speed
40180 DIM ENWQRY(10):RSEED=0            'rose to choose, random seed
40190 DIM ENMWLI(2,2,2)     		    'ENEMIES WIDTH,LEVEL,ID to seek
'
40300 DIM TTENMW(2,2)                   'TOTAL enemies for each width/level
40310 TTENMW(1,1)=2                     'HOWM monsters WITH width 1 level 1 
40320 TTENMW(2,1)=1                     'HOWM monsters WITH width 2 level 1
40330 TTENMW(1,2)=1						'HOWM monsters WITH width 1 level 2
40340 TTENMW(2,2)=2                     'HOWM monsters WITH width 2 level 2 
40350 ENMWLI(1,1,1)=HIMP:ENMWLI(1,1,2)=MANDRD
40360 ENMWLI(2,1,1)=INFOGRAM
40380 ENMWLI(1,2,1)=BADFLWR:ENMWLI(2,2,1)=SLIME:ENMWLI(2,2,2)=SKEL

40389 RETURN 'to 101

40490 '
40495 ' **** END READ ENEMIES

'
40500 ' **** COMBAT
      ' extract enemies
      '
40501 'K$=INKEY$:RSEED=RND:IF K$="" THEN GOTO 40501
40502 'CLS:SCREEN 7,0,0:CONSOLE 16,24,0:ATTRB 1,1
40503 FCHN=10
40519 ENMLMT=3
40520 ISBTL=1:TTENW=0:MXENMGEN=1+INT(RSEED+RND*(ENMLMT)): IF MXENMGEN>ENMLMT THEN MXENMGEN=ENMLMT 'MIN 1, MAX 3
40530 T=1:MXENW=6:MXENSW=2 'MXENW=Max TOTAL Width of gen. MXENSW=SINGLE MAX WIDTH
40540 RSEED=RND:IF MXENSW=1 THEN EFND=1 ELSE EFND=1+INT(SEED+RND*(MXENSW))
40550 ENWQRY(T)=EFND:TTENW=TTENW+EFND:MXENW=MXENW-EFND:MXENMGEN=MXENMGEN-1:IF MXENMGEN<=0 OR MXENW<=0 THEN 40570
40560 T=T+1:GOTO 40540
40570 HOWMENM=T:IF BETM(3)=3 THEN HOWMENM=1:TTENW=1 'BOSS?

'T=1
'MAXENM=8        'MAXENM=MAX ENEMIES TO GENERATE 
'ECNT=8:SEED=RND 'ECNT=MAX WIDTH OF SINGLE GENERATE ENEMY
'AGAIN IF (ECNT=1) THEN FND=1 ELSE FND=INT(SEED*(2)+1)
'ENWQRY(T)=FND:T=T+1
'ECNT=ECNT-FND:MAXENM=MAXENM-1
'IF ECNT>0 AND MAXENM>0 THEN AGAIN

'LOOK INTO ENWQRY TO GET WIDTH TO FIND ENEMY
'SCROLL ENEMY LIST AND GET ENEMY BY LEVEL AND
'WITDH
'PUT EACH FOUND IN THE FOUND LIST
'RANDOMIZE ON FOUND LIST

40580 FLEE=0:EGLD=0:GNXP=0:XALGN=0:TMPLEVEL=TILENM(INT((PLRPSX+1)/3),INT((PLRPSY+1)/3),1):LOCATE 1,1,0
40585 FOR I=1 TO 7:ENMSCR(I)=0:ENMSHP(I)=0:ENMSSP(I)=0:NEXT
40590 FOR I=1 TO HOWMENM:RSEED=RND	  
         'Setup enemies 
40595 IF BETM(3)=3 THEN ENMSCR(I)=ETMVL1(ETMS(PLRPSX,PLRPSY,PLRDIR,1)):GOTO 40600
40596 RENM=1+INT(SEED+RND*(TTENMW(ENWQRY(I),TMPLEVEL))):ENMSCR(I)=ENMWLI(ENWQRY(I),TMPLEVEL,RENM)
'40597 IF ENMSCR(I)=0 THEN PRINT ENWQRY(I);TMPLEVEL;RENM;:END 
40600 ENMSHP(I)=ENMHP(ENMSCR(I)):ENMSSP(I)=ENMSP(ENMSCR(I))
	     '**** ENTRANCE: Draw Enemies
40620 XDRENM=ENMGW(ENMSCR(I)):YDRENM=ENMGH(ENMSCR(I)):ENMSTR$=ENMGR$(ENMSCR(I))
40630 LXENM=12-(INT(0.5+ENMGW(ENMSCR(I))/2))-(TTENW)+XALGN:LYENM=15:XALGN=XALGN+ENMGW(ENMSCR(I))*2
40635 ENMSLX(I)=LXENM:ENMSLY(I)=LYENM
40640 COLOR ENMCL(ENMSCR(I)):GOSUB 49000
40645 PLAY"A0L1O1DOL2O2DOL3O3DOL20O4DOL1MIFAMIFA"
40650 NEXT
         ' **** COMBAT MENU
40750 ATTRB 0,0:COLOR CNORM:LOCATE 0,24,0:PRINT:PRINT
40800 XTX=0:TX$="MONSTERS are here!!":PRINT:GOSUB 48500
40850 XTX=0:TX$="PARTY wants to:": GOSUB 48500:PRINT
40890 XTX=0:COLOR CWHITE:TX$="[1]":GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$="Combat ":GOSUB 48507
40895 XTX=XTX+2:COLOR CWHITE:TX$="[2]":GOSUB 48507::XTX=XTX+1:COLOR CNORM:TX$="Avoid":GOSUB 48500:LOCATE 0,23,1
40900 K$=INKEY$:IF K$="" THEN GOTO 40900
41000 IF ASC(K$)<49 OR ASC(K$)>50 THEN GOTO 40900
41010 PLAY OK$:IF K$=CHR$(50) THEN GOTO 41030 ELSE IF K$=CHR$(49) THEN GOTO 41100
41020 GOTO 40900 'Nothing pressed
41030 'RUN
41040 GOSUB 44300:IF FLEE=1 THEN RETURN
41100 PRINT:COLOR CWHITE:PRINT">>> COMBAT! <<<":COLOR CNORM:PRINT:PRINT:ISSPMN=0
41105 FOR PRT=1 TO HOWMPLR
41110 I=PRT:COLOR 2:GOSUB 18000:COLOR CNORM:LOCATE 0,24,0:PRINT:COLOR 2:PRINT PLRNM$(PRT);:COLOR CNORM:PRINT " wants to:":PRINT:LOCATE 0,24,1
41120 COLOR CWHITE:PRINT"[1]";:COLOR CNORM:PRINT"Attack   ";
41130 IF PRT=1 THEN PRINT ELSE COLOR CWHITE:PRINT"[2]";:COLOR CNORM:PRINT"Cast Spell"
41140 COLOR CWHITE:PRINT"[3]";:COLOR CNORM:PRINT"Use Item ";
41150 COLOR CWHITE:PRINT"[4]";:COLOR CNORM:PRINT"Flee With Party":PRINT:LOCATE 0,23,1
41160 K$=INKEY$:RSEED=RND:IF K$="" THEN GOTO 41160
41165 IF(ASC(K$))<49 OR (ASC(K$))>52 THEN GOTO 41160
41170 MNO=0:PLAY OK$:KP=ASC(K$)-48:ON KP GOSUB 41300,41500,41800,42000 'CHOICES
41175 IF MNO=1 THEN 41160 ELSE IF MNO=2 THEN GOTO 41110
41180 I=PRT:COLOR 7:GOSUB 18000:COLOR CNORM
41190 NEXT PRT
41200 'PRINT:PRINT
41210 IF FLEE=1 THEN RETURN ELSE GOSUB 43000 'battle results
41220 IF TTE>0 AND FLEE=0 THEN LOCATE 0,24:PRINT:GOTO 41105 'If !monsters died AND !flee BACK TO COMBAT MENU
41260 COLR=CWALL:GOSUB 11200 'end combat: REDRAW LAB
41270 RETURN 'TO LAB VIEW      
41270 RETURN 'TO LAB VIEW      

41300 'ATTACK menu
41310 WARCH(1,PRT)=1:KPA=1:IF HOWMENM=1 THEN 41390
41350 LOCATE 0,24,1:PRINT:PRINT"ATTACK - Please select which enemy :":PRINT
41360 FOR I=1 TO HOWMENM:IF ENMSHP(I)<=0 THEN 41365 ELSE COLOR CNORM:PRINT STR$(I)+") - ";:COLOR ENMCL(ENMSCR(I)):PRINT ENMNM$(ENMSCR(I)):COLOR CNORM
41365 NEXT
41370 KA$=INKEY$:RSEED=RND:IF KA$="" THEN 41370
41380 KPA=ASC(KA$)-48:IF KPA<1 OR KPA>HOWMENM THEN 41370
41385 IF ENMSHP(KPA)<=0 THEN 41370
41390 WARCH(2,PRT)=KPA:PLAY OK$
41490 RETURN 
41500 'CAST SPELL 
41510 IF PRT>1 AND PLRMP(PRT)>2 THEN GOTO 41530 'IF PLR CAN CAST AND HAS ENOUGH MP
41520 PLAY"A0O3L18DO":K$="":MNO=1:RETURN
41530 LOCATE 0,24,1:PRINT:PRINT"CAST SPELL - Please select which spell    (B for back) :":PRINT:HOWMSP=0:MGSL=0:MGCHS(PRT-1)=0
41540 FOR M=1 TO 7:MGCSCR(PRT-1,M)=0
41545 IF MGCLV(PLRMGS(PRT-1,M))<=PLRLV(PRT-1) THEN MGCSCR(PRT-1,M)=PLRMGS(PRT-1,M):PRINT STR$(M)+") - ";:COLOR 7:PRINT MGCNM$(MGCSCR(PRT-1,M));:COLOR CNORM:PRINT"(";MGCMP(MGCSCR(PRT-1,M));"MP )":HOWMSP=HOWMSP+1
41550 NEXT
41560 KA$=INKEY$:RSEED=RND:IF KA$="" THEN 41560
41565 IF ASC(KA$)=66 THEN KA$="":K$="":MNO=2:RETURN 'B=BACK
41570 KPA=ASC(KA$)-48:IF KPA<1 OR KPA>HOWMSP THEN 41560
41575 IF ISSPMN=1 AND MGCKND(MGCSCR(PRT-1,KPA))=1 THEN PLAY "A0O3L18DO":MNO=1:RETURN 'NO COMBAT SPELL OUTSIDE COMBAT
41580 IF MGCMP(MGCSCR(PRT-1,KPA)) <= PLRMP(PRT) THEN 41590 ELSE PLAY"A0O3L18DO":GOTO 41560
41590 MGSL=MGCSCR(PRT-1,KPA):IF MGCMXLV(MGCSCR(PRT-1,MGSL))>1 THEN GOTO 41600 ELSE MGSL=1:KPA=1:GOTO 41660
      'SELECT MAGIC LEVEL
41600 PLAY OK$:HOWMSP=0:LOCATE 0,24,1:PRINT:PRINT"Please select spell level for ";:COLOR 7:PRINT MGCNM$(MGCSCR(PRT-1,MGSL));:COLOR CNORM:PRINT "  (B for back) :":PRINT:
41610 FOR M=1 TO MGCMXLV(MGCSCR(PRT-1,MGSL)):IF PLRLV(PRT)>=MGCLV(MGCSCR(PRT-1,MGSL))*M THEN HOWMSP=HOWMSP+1:PRINT STR$(M)+") - Level";M;"(";M*MGCMP(MGCSCR(PRT-1,MGSL));"MP )"
41620 NEXT
41630 KA$=INKEY$:RSEED=RND:IF KA$="" THEN 41630
41635 IF ASC(KA$)=66 THEN KA$="":K$="":MNO=2:RETURN 'B=BACK
41640 KPA=ASC(KA$)-48:IF KPA<1 OR KPA>HOWMSP THEN 41630
41650 IF MGCMP(MGCSCR(PRT-1,MGSL))*KPA <= PLRMP(PRT) THEN 41660
41655 PLAY"A0O3L18DO":GOTO 41630 'NOT ENOUGH MP
41660 MGCSPW(PRT-1,MGSL)=KPA:MGCHS(PRT-1)=MGSL
41690 WARCH(1,PRT)=2:PLAY OK$ 'CAST SPELL
      'IF MGCTSPEC 1..8=ENEMIES, 9=ALL ENEMS/PLR, 10,11,12=PLR1,2,3 13=ALL PLRS 
41700 IF KPA>=3 AND MGCKND(MGCSCR(PRT-1,MGSL))=1 THEN GOTO 41785 ELSE IF KPA>=3 AND MGCKND(MGCSCR(PRT-1,MGSL))=3 THEN GOTO 41780 'ALL ENEMIES/PARTY
41705 IF HOWMENM=1 THEN KPA=1:GOTO 41750
41710 LOCATE 0,24,1:PRINT:PRINT"Please select target for ";:COLOR 7:PRINT MGCNM$(MGCSCR(PRT-1,MGSL));:COLOR CNORM:PRINT":":PRINT
41720 IF MGCTSPEC(MGCSCR(PRT-1,MGSL))>9 THEN GOTO 41760 'PLAYERS
      'ENEMIES
41725 FOR I=1 TO HOWMENM:IF ENMSHP(I)<=0 THEN 41730 ELSE COLOR CNORM:PRINT STR$(I)+") - ";:COLOR ENMCL(ENMSCR(I)):PRINT ENMNM$(ENMSCR(I)):COLOR CNORM
41730 NEXT
41735 KA$=INKEY$:RSEED=RND:IF KA$="" THEN 41735
41740 KPA=ASC(KA$)-48:IF KPA<1 OR KPA>HOWMENM THEN 41735 
41750 WARCH(3,PRT)=KPA:PLAY OK$:GOTO 41790 'SET ENEMY
41760 HMP=1:FOR I=1 TO HOWMPLR:IF PLRHP(I)>0 THEN PRINT STR$(I)+") - ";:COLOR 2:PRINT PLRNM$(I):HMP=HMP+1:COLOR CNORM 'PLAYERS
41762 NEXT
41765 KA$=INKEY$:RSEED=RND:IF KA$="" THEN 41765
41770 KPA=ASC(KA$)-48:IF KPA<1 OR KPA>HMP THEN 41765
41775 WARCH(3,PRT)=KPA:PLAY OK$:GOTO 41790 'SET PLAYER
41780 WARCH(3,PRT)=13:GOTO 41790 'SET ALL PLRS
41785 WARCH(3,PRT)=9 'SET ALL ENEMIES
41790 RETURN
'
41800 WARCH(1,PRT)=3'USE ITEM
41805 PRINT:PRINT"Please select the item:":PRINT:GOSUB 58000 'SELECT ITEM
41810 IF CITM>0 THEN WARCH(2,PRT)=CITM ELSE MNO=2:RETURN
41890 PLAY OK$:RETURN
'
42000 WARCH(1,PRT)=4'FLEE
42090 RETURN
42800

43000 '
43005 'BATTLE RESULT
43010 '
43020 LOCATE 0,24,0:PRINT
43100 FOR T=1 TO 10:TURNISP(T)=0:NEXT
43110 FOR T=1 TO HOWMPLR:TURNISP(T)=(PLRSP(T)*1000)+T:NEXT
43120 FOR T=1 TO HOWMENM:TURNISP(T+3)=(ENMSSP(T)*1000)+3+T:NEXT
43130 'bubble sort
43140 FLAG=0:FOR T=1 TO 9
43150 IF TURNISP(T)=>TURNISP(T+1) THEN GOTO 43170
43160 SWAP TURNISP(T),TURNISP(T+1):FLAG=1
43170 NEXT T
43180 IF FLAG=1 THEN GOTO 43140
43190 'execute each move: 
43200 FOR T=1 TO 10
43210 'if is a player it ends by 1,2,3 otherwise monster
43220 HIM=VAL(RIGHT$(STR$(TURNISP(T)),1))
43230 IF HIM=0 THEN GOTO 46000 ELSE IF HIM>3 THEN GOTO 44600
43500 'Party movement
43510 '1: attack, 2: spell, 3: use item, 4: flee
43520 ON WARCH(1,HIM) GOSUB 43530,43800,44000,44300
      'VICTORY?
43521 TTE=0:FOR I=1 TO HOWMENM:TTE=TTE+ENMSHP(I):NEXT 
43522 IF TTE>0 THEN GOTO 46000
43523 LOCATE 0,24,1:COLOR 2:SCREEN,,2:PRINT:PRINT ANK$+" PARTY HOWLS FOR VICTORY !!! "+ANK$
43524 PLAY"A1L16O4SIPSISISIL16PO5DO#PO5L06RE#DO#RE#DO#RE#DO#RE#DO#RE#DO#RE#DO#RE#":SCREEN,,0: '!!!!! VICTORY !!!!!!
43525 COLOR CNORM:PRINT "Experience Gained:";GNXP:PRINT "Gold Earned:";EGLD:PLRGLD=PLRGLD+EGLD:PRINT:GOSUB 18100  'GOLD, XP, MUSIC AND SO ON
43526 FOR T=1 TO HOWMPLR:PLRXP(T)=PLRXP(T)+GNXP:IF PLRXP(T)>=PLRNXP(T) THEN GOSUB 49300 'LEVEL UP
43527 NEXT:RETURN
      '
43528 GOTO 46000 'MOVE, GOTO NEXT ELEMENT
43530 'ATTACK
43560 ENM=WARCH(2,HIM):IF ENMSHP(ENM)>0 THEN GOTO 43600
43570 ENM=0:FOR I=1 TO HOWMENM:IF ENMSHP(I)>0 THEN ENM=I 'SEARCH ANOTHER ENEMY 
43575 NEXT:IF ENM=0 THEN GOTO 43790 
43600 LOCATE 0,24,0:PRINT:PRINT:XTX=0:COLOR 2:TX$=PLRNM$(HIM):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" attacks ":GOSUB 48507
43605 XTX=XTX+1:COLOR ENMCL(ENMSCR(ENM)):TX$=ENMNM$(ENMSCR(ENM)):GOSUB 48507:COLOR CNORM
43610 'roll 20, 0 miss, 20 critical hit; add speed+weapon bonus+(lv/2)
43620 RSEED=INT(RND*21):APP=PLRAP(HIM):ATP=PLRAP(HIM)+RSEED:DFP=ENMDP(ENMSCR(ENM)):DMG=0
43630 IF RSEED=0 THEN GOTO 43660 ELSE IF RSEED=20 THEN GOTO 43670 'IS MISS/CRITICAL?
43640 'sum(enemy dp+speed): if it is < plr attack turn point, we have a hit
43650 IF ATP+(PLRLV(HIM)/2)+PLRSP(HIM)>DFP+ENMSP(ENMSCR(ENM)) THEN GOTO 43680
43660 PRINT:XTX=0:TX$="The attack FAILS!":GOSUB 48500::PLAY"A0L80PP"::RETURN
43670 DMG=(APP*(APP/(APP+DFP)))+PLRLV(HIM)+INT(RND*(PLRLV(HIM))):PLAY"A0L12O5DOFALA":PRINT:XTX=0:TX$="AWESOME BLOW !!!":COLOR 2:GOSUB 48500:COLOR 3 'CRITICAL HIT
43680 RSEED=RND:DMG=1+DMG+(APP*(APP/(APP+DFP)))+PLRLV(HIM)-ENMLV(ENMSCR(ENM))+INT((RSEED+RND*(PLRLV(HIM)))+1):DMG=INT(DMG):PLAY"A0L4O5DOO4DOO3DOO2DOO1DO"
43690 CHITTED=1:GOSUB 47000 'HITTED ENEMY
43790 RETURN
43800 'SPELL
43810 LOCATE 0,24,0:PRINT:PRINT:XTX=0:COLOR 2:TX$=PLRNM$(HIM):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" casts ":GOSUB 48507:XTX=XTX+1
43815 COLOR 7:TX$=MGCNM$(MGCHS(HIM-1)):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" level"+STR$(MGCSPW(HIM-1,MGCHS(HIM-1))):GOSUB 48507
43830 PLRMP(HIM)=PLRMP(HIM)-(MGCMP(MGCHS(HIM-1))*(MGCSPW(HIM-1,MGCHS(HIM-1)))):GOSUB 17000:FOR I=1 TO 16:IF I MOD 2=0 THEN 43860
43850 SCREEN,,MGCSC1(MGCHS(HIM-1)):PLAY MGCSND$(MGCHS(HIM-1)):GOTO 43870
43860 SCREEN,,MGCSC2(MGCHS(HIM-1)):PLAY MGCSND$(MGCHS(HIM-1))
43870 NEXT:SCREEN,,0
      'EFFECTS KIND:
	  'DAMAGE,SETSTATUS,HEAL,REVIVE 	  	  
43880 IF MGCKND(MGCHS(HIM-1))=1 THEN 43890 ELSE IF MGCKND(MGCHS(HIM-1))=2 THEN 43900 ELSE IF MGCKND(MGCHS(HIM-1))=3 THEN 43910 'DAMAGE ENEMY,STATUS,HEAL
43890 DMG=MGCVAL(MGCHS(HIM-1))+PLRLV(HIM)+PLRMGP(HIM):CHITTED=MGCSC1(MGCHS(HIM-1)):ENM=WARCH(3,HIM)
43892 IF ENM<8 THEN 43895 ELSE FOR I=1 TO HOWMENM:IF ENMSHP(I)>0 THEN ENM=I:GOSUB 47000:NEXT:GOTO 43990 'DAMAGE ALL
43895 IF ENMSHP(ENM)>0 THEN GOSUB 47000:GOTO 43990 'DAMAGE SINGLE
43897 ENM=0:FOR I=1 TO HOWMENM:IF ENMSHP(I)>0 THEN ENM=I:GOSUB 47000:GOTO 43990
43898 NEXT 'SEARCH ANOTHER ENEMY
43900 GOTO 43990 'SET STATUS
43910 PHT=WARCH(3,HIM):RCV=MGCVAL(MGCHS(HIM-1))+PLRLV(HIM)+PLRMGP(HIM):IF(PHT=13)THEN FOR I=1 TO HOWMENM:PHT=I:GOSUB 48380:NEXT:GOTO 43990 ELSE GOSUB 48380:GOTO 43990 'HEAL
43990 RETURN
44000 'ITEM
44005 PRINT:CITM=WARCH(2,HIM):PRT=HIM:GOSUB 57220
44290 RETURN
44300 'FLEE
44305 PRINT:LOCATE 0,24,0:PRINT:XTX=0:TX$="PARTY tries to dodge the fight and ... ":GOSUB 48500
44310 FOR I=1 TO HOWMENM:GOSUB 47060:NEXT I
44315 FLEE=1:COLOR 2:TX$="Succeeds!":GOSUB 48500:PLAY"A0L6O4LASI":COLR=CWALL: GOSUB 11200:COLOR CNORM:LOCATE 0,24,1:PRINT:RETURN 'TO COMBAT MENU->LAB VIEW
44320 FLEE=0:PLAY"A0L8O2DOMIFAL16RE":COLOR 9:PRINT:TX$="Can't run!":GOSUB 48500:PLAY"PPPP":COLOR CNORM
44590 RETURN
44600 'MONSTER MOVEMENT
44610 'GET MONSTER, ROLL FOR DECISION, ROLL FOR RESULT, ROLL FOR DAMAGE
44620 ENM=HIM-3:IF ENMSHP(ENM)<=0 THEN GOTO 46000
44640 COLOR 11:ATTRB 1,1:XDRENM=ENMGW(ENMSCR(ENM)):YDRENM=ENMGH(ENMSCR(ENM)):ENMSTR$=ENMGR$(ENMSCR(ENM)):LXENM=ENMSLX(ENM):LYENM=ENMSLY(ENM)
44650 GOSUB 49000:ATTRB 0,0
44700 LOCATE 0,24,0:PRINT:PRINT:XTX=0:COLOR ENMCL(ENMSCR(ENM)):TX$=ENMNM$(ENMSCR(ENM)):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" attacks!":GOSUB 48507
44750 PHT=1+RND*(HOWMPLR-1):GOSUB 48200'PLR DAMAGED
44770 COLOR ENMCL(ENMSCR(ENM)):ATTRB 1,1:XDRENM=ENMGW(ENMSCR(ENM)):YDRENM=ENMGH(ENMSCR(ENM)):ENMSTR$=ENMGR$(ENMSCR(ENM)):LXENM=ENMSLX(ENM):LYENM=ENMSLY(ENM)
44780 GOSUB 49000:ATTRB 0,0
45990 GOTO 46000
46000 NEXT T

46810 RETURN 'END BATTLE RESULTS, RETURN TO 41210
  
' Test attack
' XDRENM=XSKEL:YDRENM=YSKEL:ENMSTR$=SKEL$
' LXENM=10:LYENM=10
' FOR X=1 TO 3:IF CURCLR=ENMCL(SKEL) THEN CURCLR=CHIT ELSE CURCLR=ENMCL(SKEL)
' COLOR CURCLR:GOSUB 49000:NEXT:COLOR CNORM
'
' ATTRB 0,0
' XTX=0:TX$="SKELETON 1 attacks!": GOSUB 48500
' FOR S=1 TO 10:SCREEN,,1:PLAY"L1O1DO":SCREEN,,0:NEXT S:PLAY"L3O2DOO3REO3DO"
' XTX=0:TX$="PALADIN suffers 1 POINTS of damage.": GOSUB 48500
' GOTO 49500




      ' ***** 
      'ROUTINES
      'ENEMY DAMAGED
47000 ATTRB 1,1:FOR DR=1 TO 2:IF DR MOD 2=1 THEN COLOR CHITTED ELSE COLOR ENMCL(ENMSCR(ENM)) 'draw hitted enemy
47010 IF DR MOD 2=1 THEN SCREEN,,CHITTED ELSE SCREEN,,0
47015 XDRENM=ENMGW(ENMSCR(ENM)):YDRENM=ENMGH(ENMSCR(ENM)):ENMSTR$=ENMGR$(ENMSCR(ENM)):LXENM=ENMSLX(ENM):LYENM=ENMSLY(ENM):PLAY"A0L1O5SI"
47020 GOSUB 49000:NEXT DR:ATTRB 0,0:ENMSHP(ENM)=ENMSHP(ENM)-DMG 'APPLY DAMAGE
47030 COLOR CNORM:LOCATE 0,24,0:PRINT:XTX=0:COLOR ENMCL(ENMSCR(ENM)):TX$=ENMNM$(ENMSCR(ENM)):GOSUB 48507
47035 XTX=XTX+1:COLOR CNORM:TX$=" suffers"+STR$(DMG)+" damage points.":GOSUB 48507
47040 IF ENMSHP(ENM)>0 THEN RETURN
      'ENEMY DIED
47050 ENMSHP(ENM)=0:I=ENM:GOSUB 47060:COLOR CNORM:LOCATE 0,24,0:PLAY"A05L8SISOL12DO"
47055 PRINT:XTX=0:COLOR ENMCL(ENMSCR(ENM)):TX$=ENMNM$(ENMSCR(ENM)):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" is dead!":GNXP=GNXP+ENMXP(ENMSCR(ENM)):EGLD=EGLD+ENMGD(ENMSCR(ENM)):GOSUB 48507:IF BETM(3)=3 THEN ETMID(ETMS(PLRPSX,PLRPSY,PLRDIR,1))=0
47057 RETURN
	  'ERASE ENEMY
47060 COLOR 0:ATTRB 1,1:XDRENM=ENMGW(ENMSCR(I)):YDRENM=ENMGH(ENMSCR(I)):ENMSTR$=ENMGR$(ENMSCR(I))
47070 LXENM=ENMSLX(I):LYENM=ENMSLY(I):BOXF(LXENM*8,(LYENM*8)-((YDRENM+1)*16))-((LXENM*8)+(XDRENM*16),(LYENM*8)),-1:ATTRB 0,0:COLOR CNORM	  
47080 RETURN	  

	  'ENEMY HEALED, 
	  'ENEMY SETSTATUS, 
	  'ENEMY REVIVE (CALL 4 HELP)	
	  'PLAYER SETSTATUS, 
	  'PLAYER REVIVE, 
	  'EXIT, 
	  'MAP	

      'USE	  
	  'PLAYER DAMAGED, 
48200 FOR DR=1 TO 7:IF DR MOD 2=1 THEN SCREEN,,0 ELSE SCREEN,,1
48210 IF DR MOD 2=1 THEN COLOR 2,0 ELSE COLOR 0,1:I=PHT:GOSUB 18000
48220 LOCATE 23,LHPY(PHT):PRINT PLRNM$(PHT):PLAY"A0L3O1MI":NEXT
48225 COLOR7:I=PHT:GOSUB 18000:COLOR CNORM:LOCATE 0,24,0
48230 PRINT:COLOR 2:XTX=0:TX$=PLRNM$(PHT):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" suffers!":GOSUB 48507
48300 RETURN

	  'PLAYER HEALED,
48380 IF PLRHP(PHT)<=0 THEN 48415 
48385 FOR DR=1 TO 7:IF DR MOD 2=1 THEN SCREEN,,0 ELSE SCREEN,,2
48390 IF DR MOD 2=1 THEN COLOR 2,0 ELSE COLOR 0,2:I=PHT:GOSUB 18000
48395 LOCATE 23,LHPY(PHT):PRINT PLRNM$(PHT):PLAY"A2L3O5SI":NEXT
48400 COLOR7:I=PHT:GOSUB 18000:COLOR CNORM:LOCATE 0,24,0
48405 IF PLRHP(PHT)+RCV>PLRMXHP(PHT) THEN RCV=PLRMXHP(PHT)-PLRHP(PHT)
48410 LOCATE 0,24,0:PRINT:COLOR 2,0:XTX=0:TX$=PLRNM$(PHT):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" recovers"+STR$(RCV)+" health points.":GOSUB 48507
48415 RETURN 	
	
48500 '
48501 '*********** CONSOLE PRINT
48502 '
48505 TX$=TX$+CHR$(13)+CHR$(10)
48507 CRS=1
48510 LOCATE XTX,23,0:PRINT MID$(TX$,CRS,1)
48520 CRS=CRS+1:IF CRS>LEN(TX$) THEN RETURN
48530 XTX=XTX+1:IF XTX>39 THEN XTX=0
48540 GOTO 48510 
49000 '
49005 '*********** DRAW ENEMIES
49006 '
49010 ATTRB 1,1:XDRAW=LXENM:YDRAW=LYENM-(YDRENM*2)
49020 XDCE=1
49030 FOR Y = YDRAW TO YDRAW+(YDRENM*2) STEP 2
49040 LOCATE XDRAW,Y,0:PRINT MID$(ENMSTR$,XDCE,XDRENM)
49050 XDCE=XDCE+XDRENM
49060 NEXT Y
49070 ATTRB 0,0:RETURN
49100 '*********** DRAW PNG
49110 '
49120 ATTRB 1,1:XDRAW=10:XDCE=1:FOR Y=9 TO 14 STEP 2:LOCATE XDRAW,Y,0:PRINT MID$(PNG$,XDCE,2):XDCE=XDCE+2:NEXT Y:ATTRB 0,0
49180 IF PNG$<>PLRGR$(3) THEN RETURN 
49190 PLAY"A1O4L8MIFALA":COLOR CNORM:LOCATE 0,24,0:PRINT:PRINT:XTX=0:TX$=CHR$(34)+"It seems MOLUOC found a brave hero.":GOSUB 48500:PLAY"A0O1L64PP":XTX=0:TX$=" I'm DUPLEX. Thanks for saving me.":GOSUB 48500:PLAY"A0O1L64PP"
49195 XTX=0:TX$=" I found the key for the next floor.":PLAY"A0O1L64PP":GOSUB 48500:XTX=0:TX$=" But let's have a beer before,":GOSUB 48500:XTX=0:TX$=" i'll tell you the whole story."+CHR$(34):GOSUB 48500:PLAY"A0O1L64PP"
49200 PRINT:PRINT:PRINT:XTX=0:COLOR 2:TX$="DUPLEX":GOSUB 48507::XTX=XTX+1:COLOR CNORM:TX$=" JOINS THE PARTY !!":GOSUB 48507:ETMID(ETMS(PLRPSX,PLRPSY,PLRDIR,1))=0:BETM(3)=0
49210 HOWMPLR=3:GOSUB 17000:SCREEN,,2:PLAY"A1L16O4SIPSISISIL16PO5DO#PO5L06RE#DO#RE#DO#RE#DO#RE#DO#RE#DO#RE#DO#RE#":SCREEN,,0
49230 BOXF(80,64)-(160,115),-1:COLOR CWALL:GOSUB 11200:COLOR CNORM:LOCATE 0,24,1:PRINT:RETURN
49300 '*** PLR LEVEL UP
49310 PLRLV(T)=PLRLV(T)+1:PLRMXHP(T)=PLRMXHP(T)+PLRLV(T):PLRMXMP(T)=PLRMXMP(T)+PLRLV(T):PLRNXP(T)=PLRNXP(T)+(PLRNXP(T)+INT(PLRXP(T)/(T+1))+PLRLV(T)):PLRAP(T)=PLRAP(T)+PLRLV(T):PLRDP(T)=PLRDP(T)+PLRLV(T):PLRSP(T)=PLRSP(T)+PLRLV(T)
49320 PRINT:XTX=0:COLOR 2:TX$=PLRNM$(T):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" GOES LEVEL ":GOSUB 48507:COLOR 7:XTX=XTX+1:TX$=STR$(PLRLV(T)):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" !!":GOSUB 48507
49330 GOSUB 17000:LOCATE 0,24,0:PLAY"A1O4L12FALASIO5DOREL8DOREDOREDOREDOREL12SOLASI":RETURN
'
49500 CONSOLE 0,24,0:END
	  '
50000 LOCATE 0,3:PRINT "Building labyrinth ..."
      'LABIRINTH ITEMS
51000 MAPW=9:MAPH=9:DIM MAP(MAPW,MAPH,1):MX=1
51005 DATA 1,"DOOR",2,"CHEST",3,"BOSS",4,"PERSON",5,"WRITING"
51010 FOR I=1 TO 5:READ ETMID(I):READ ETMNM$(I):NEXT
      'W,H,DIRS,LEVELS
51015 DIM ETMS(MAPW,MAPH,4,3):TTETMS(1)=8
      'DATA ID,PX,PY,DIR,VAL1,VAL2
	  'LV1:
51020 DATA 1,8,2,1,0,0:DATA 2,5,2,4,0,2:DATA 3,7,8,4,7,0:DATA 4,6,8,4,3,0:DATA 2,4,8,3,0,10020:DATA 2,2,4,4,0,1:DATA 2,2,2,4,0,10005:DATA 1,2,8,3,1,0
51030 FOR I=1 TO TTETMS(1):READ ETMID(I):READ X:READ Y:READ XYDIR:READ ETMVL1(I):READ ETMVL2(I):ETMS(X,Y,XYDIR,1)=I:NEXT
	  'How to use: NAME=ETMNM$(ETMID(ETMS(X,Y,XYDIR,1)))
      'How to use: VAL=EVL1(ETMS(X,Y,XYDIR,1))      
52000 'LABIRINTH LV1
52005 DATA 7,7,7,4,4,5,6,6,5,4,2,5,6,0,1,4,5,5,5,1,5,5,2,1,7,7,7
52010 DIM ZIPM(3,9,1)
52015 FOR Y=1 TO 9:FOR X=1 TO 3:READ ZIPN(X,Y,1):NEXT X:NEXT Y
'LAB WIDTH, LAB HEIGHT, LAB LEVEL
52050 FOR Y=1 TO MAPH:FOR X=1 TO 3
52060 DN=ZIPN(X,Y,1)
52100 LOCATE 22,3:PRINT MAPH-Y+1 'PROGRESS
52140 MAP(MX,Y,1)=-1*((DN AND 4) > 0)   
52150 MAP(MX+1,Y,1)=-1*((DN AND 2) > 0) 
52160 MAP(MX+2,Y,1)=-1*((DN AND 1) > 0) 
52380 MX=MX+3
52410 NEXT X:MX=1:NEXT Y
52440 'enemies level for each 3x3 block:
52450 DIM TILENM(3,3,1)
52460 TILENM(1,1,1)=1:TILENM(2,1,1)=2:TILENM(3,1,1)=2:TILENM(1,2,1)=1:TILENM(2,2,1)=1:TILENM(3,2,1)=2:TILENM(1,3,1)=1:TILENM(2,3,1)=2:TILENM(3,3,1)=2
52470 'NEXT
54880 CLS
54900 RETURN
55000 'MAPPA
55005 PRINT:PRINT "Unrolling map ..."
55010 BOXF(0,10)-(180,120),-1
55020 LXM=3:LYM=3
55030 FOR Y=1 TO 9:FOR X=1 TO 9
55040 IF MAP(X,Y,1)=0 THEN GOTO 55060
55050 LOCATE LXM+X,LXM+Y,0:COLOR 7:PRINT CHR$(127)
55060 NEXT:NEXT
55070 K$=INKEY$
55075 LOCATE LXM+PLRPSX,LYM+PLRPSY,0:COLOR 1:PRINT CHR$(127)
55080 IF (K$="") THEN GOTO 55070
55090 BOXF(0,10)-(180,120),-1
55100 CONSOLE 16,24,0:COLOR 3:LOCATE 0,24,1
55110 COLR=CWALL:CCHEST=15:GOSUB 11200:RETURN 'DRAW LAB, RETURN
55200 'spell menu
55210 ISSPMN=1:IF HOWMPLR>2 AND PLRHP(2)>0 AND PLRHP(3)>0 THEN 55230
55215 IF HOWMPLR<2 OR (PLRHP(2)=0 AND PLRHP(3)=0) THEN COLOR 9:PRINT:PRINT "No magical players in party.":PLAY"A0L64PPP":RETURN
55220 IF HOWMPLR=2 OR PLRHP(3)=0 THEN PRT=2:PRINT:GOTO 55265 ELSE PRT=3:PRINT:GOTO 55265
55230 PMIN=2:PMAX=3:LOCATE 0,24,1:PRINT:PRINT:PRINT"Please select which player would"+CHR$(13)+CHR$(10)+"cast the spell:":PRINT:GOSUB 56000
55265 MNO=0:GOSUB 41500:IF MNO=2 THEN RETURN 'PRESSED BACK
55270 IF MNO=1 THEN PRINT:PRINT"Not here, not now.":RETURN'BACK TO PROMPT
55280 HIM=PRT:PHT=PRT:GOSUB 43800:RETURN 'EXEC Spell, back
55300 'stats menu
55310 PMIN=1:PMAX=HOWMPLR:LOCATE 0,24,1:PRINT:PRINT:PRINT"STATS - Please select which player:":PRINT:GOSUB 56000
55320 PLAY OK$:PRINT:COLOR 7:PRINT"CLASS: ";:COLOR CNORM:PRINT RACE$(PRT);:COLOR 7:PRINT" EXP:";:COLOR CNORM:PRINT STR$(PLRXP(PRT));:COLOR 7:PRINT" NEXT:";:COLOR CNORM:PRINT STR$(PLRNXP(PRT))
55330 COLOR 7:PRINT"ATTACK:";:COLOR CNORM:PRINT STR$(PLRAP(PRT));:COLOR 7:PRINT" DEFENSE:";:COLOR CNORM:PRINT STR$(PLRDP(PRT));:COLOR 7:PRINT" SPEED:";:COLOR CNORM:PRINT STR$(PLRSP(PRT))
55340 COLOR 7:PRINT"WEAPON:";:COLOR CNORM:PRINT ITMNM$(EQUIP(1,PRT));:COLOR 7:PRINT" SHIELD:";:COLOR CNORM:PRINT ITMNM$(EQUIP(2,PRT))
55350 COLOR 7:PRINT"ARMOR:";:COLOR CNORM:PRINT ITMNM$(EQUIP(3,PRT));:COLOR 7:PRINT" LUCK:";:COLOR CNORM:PRINT STR$(PLRLP(PRT))
55360 COLOR 7:PRINT"STATUS:";:COLOR CNORM:PRINT STR$(PLRNXP(PRT))
55370 K$=INKEY$:IF K$="" THEN 55370
55380 'INVENTORY
55385 COLOR 2:PRINT:PRINT PLRNM$(PRT);:COLOR CNORM:PRINT"'s items:":PRINT:T=0:FOR I=1 TO 7:EQ$="":IF INVNT(I,PRT)=0 THEN 55390
55386 IF INVNT(I,PRT)=EQUIP(1,PRT) OR INVNT(I,PRT)=EQUIP(2,PRT) OR INVNT(I,PRT)=EQUIP(3,PRT) THEN EQ$="(E)"
55388 T=T+1:PRINT "- "+ITMNM$(INVNT(I,PRT))+EQ$
55390 NEXT:IF T=0 THEN PRINT:PRINT"( Nothing )"
55395 K$=INKEY$:IF K$="" THEN 55395 ELSE RETURN
56000 'player selection menu
56010 FOR I=PMIN TO PMAX:PRINT STR$(I)+") - ";:COLOR 2:PRINT PLRNM$(I):COLOR CNORM:NEXT
56020 K$=INKEY$:IF K$="" THEN 56020
56030 IF ASC(K$)<48+PMIN OR ASC(K$)>48+PMAX THEN 56020
56040 PRT=ASC(K$)-48:RETURN
57000 'ITEM MENU
57010 MNO=1:PMIN=1:PMAX=HOWMPLR:LOCATE 0,24,1:PRINT:PRINT:PRINT"ITEM - Please select which player:":PRINT:GOSUB 56000:PRINT:PLAY OK$:CITM=0:FOR I=1 TO 7:IF INVNT(I,PRT)>0 THEN CITM=CITM+1
57015 NEXT:IF CITM=0 THEN 57090 ELSE PRINT"Please select what to do:"+CHR$(13)+CHR$(10)+"(B for back)":PRINT:CITM=0
57020 COLOR CWHITE:PRINT"[1]";:COLOR CNORM:PRINT"Equip    ";
57030 COLOR CWHITE:PRINT"[2]";:COLOR CNORM:PRINT"Use       "
57040 COLOR CWHITE:PRINT"[3]";:COLOR CNORM:PRINT"Give     ";
57050 COLOR CWHITE:PRINT"[4]";:COLOR CNORM:PRINT"Discard        ":PRINT:LOCATE 0,23,1
57060 K$=INKEY$:RSEED=RND:IF K$="" THEN GOTO 57060
57070 IF ASC(K$)=66 THEN 57110 IF(ASC(K$))<49 OR (ASC(K$))>52 THEN GOTO 57060
57080 PLAY OK$:KPM=ASC(K$)-48:PRINT:PRINT"Please select the item:":PRINT:GOSUB 58000:PLAY OK$
57090 IF CITM=0 THEN COLOR 9:PRINT:PRINT"( No items )":COLOR CNORM:PRINT:PLAY"A0L32O1PPP":RETURN 'CITM=ITEM, KPM=ACTION
57100 IF (MNO=1) THEN ON KPM GOSUB 57120,57220,57420,57550 ELSE RETURN 'IF IS IN COMBAT RETURNS THE SELECTED ITEM TO USE
57110 RETURN 'TO GAME VIEW

57120 'EQUIP 
57125 IF ITMKND(CITM)>=4 OR ITMRC(CITM)<>PLRRC(PRT) THEN COLOR 9:PRINT:PRINT"Can't equip this item":PLAY"A1O1L32PP":PRINT:RETURN
57130 EQUIP(ITMKND(CITM),PRT)=ITMID(CITM):PRINT:PRINT ITMNM$(CITM)+" equipped !":PLAY"A1O5L06SOLASI":PLAY"A1O1L32PP":PRINT:RETURN
57220 'USE
      'item can be used in dungeon or not, in battle or not
      '0=everywhere,anytime(herb) 1=only external 2=only dungeon,no battle(map) 3=only dungeon,battle(weapons,shields,ecc.), 4=NEVER
57230 PRINT:PRINT:XTX=0:COLOR 2:TX$=PLRNM$(PRT):GOSUB 48507:XTX=XTX+1:COLOR CNORM:TX$=" uses ":GOSUB 48507:XTX=XTX+1:COLOR 7:TX$=ITMNM$(CITM):GOSUB 48507:COLOR CNORM:PLAY"A1O5L06SOLASI"
57240 IF ITMULC(CITM)=4 OR (ITMULC(CITM)=1 AND PLRLOC=1) OR (ITMULC(CITM)=2 AND (PLRLOC<>1 OR ISBTL=1)) OR (ITMULC(CITM)=3 AND (PLRLOC<>1 OR ISBTL=0)) THEN PLAY"A0O3L18DO":PRINT:LOCATE 0,24,0:PRINT"Not here, not now.":PRINT:PLAY"A0L64O1PP":PRINT:RETURN 	  
      'SELECT EFFECT:DAMAGE,SETSTATUS,HEAL,EXIT,OPEN DOOR,MAP,ecc..
57300 IF ITMKND(CITM)<4 THEN RETURN ELSE IF ITMFX(CITM)=6 THEN GOSUB 55000 'MAP : IF ...
57305 IF ITMFX(CITM)=3 THEN PHT=PRT:GOSUB 48380 'HEAL
57310 IF ITMONC(CITM)=1 THEN INVNT(KPA,PRT)=0 'REMOVES ITEM FROM INVNT
57419 RETURN
57420 'GIVE ('you can't give a weapon/shield/armor which target already has)
57459 RETURN
57550 'DISCARD 
57555 PRINT:PRINT"You'll lost ";:COLOR 7:PRINT ITMNM$(CITM);:COLOR CNORM:PRINT" forever!":PRINT:PRINT"- ARE YOU SURE ? -"
57560 COLOR 7:PRINT"[1]";:COLOR CNORM:PRINT"Yes";:COLOR 7:PRINT"     [2]";:COLOR CNORM:PRINT"No"
57565 K$=INKEY$:RSEED=RND:IF K$="" THEN 57565 ELSE IF K$="2" THEN PLAY OK$:PRINT:RETURN
57570 PRINT ITMNM$(CITM)+" Discarded !":INVNT(KPA,PRT)=0:FOR I=1 TO 3:IF EQUIP(I,PRT)=CITM THEN EQUIP(I,PRT)=0
57575 NEXT:PLAY"A1O5L06SOLASI":PLAY"A1O1L32PP"
57650 RETURN 'TO 57110

58000 'ITEM SELECTION
58010 CITM=0:IC=0:FOR I=1 TO 7:EQ$="":IF INVNT(I,PRT)=0 THEN 58020 
58012 IC=IC+1:ICS(IC)=ITMID(INVNT(I,PRT)):IF ICS(IC)=EQUIP(1,PRT) OR ICS(IC)=EQUIP(2,PRT) OR ICS(IC)=EQUIP(3,PRT) THEN EQ$="(E)"
58015 PRINT STR$(IC)+") "+ITMNM$(INVNT(I,PRT))+EQ$ 'GET INVENTORY FOR PLRINV(PRT)=ID OBJ
58020 NEXT: IF IC=0 THEN PRINT:PRINT"( No items )":PRINT:RETURN
58030 K$=INKEY$:RSEED=RND:IF K$="" THEN 58030 ELSE KPA=ASC(K$)-48:IF KPA<1 OR KPA>IC THEN 58030 ELSE CITM=ICS(KPA):RETURN

'*** EXIT,end
60000 CONSOLE 0,24,0
60010 POKE &H2076,7
60020 POKE &H2019,PEEK(&H2019) AND &HF7
60100 END

